#!/usr/local/bin/otclsh

if {$argc != 3} {
   puts "usage: $argv0 id ns_input_file ir_file"
   exit 1
}
set id [lindex $argv 0]
set nsfile [lindex $argv 1]
set irfile [lindex $argv 2]

set tbroot @prefix@

set libdir @prefix@/lib/ns2ir
set tbcompat "$libdir/tb_compat.tcl"
source $libdir/tcl-object.tcl
source $libdir/node.tcl
source $libdir/link.tcl
source $libdir/event.tcl
source $libdir/lan.tcl

###
# calfeld@cs.utah.edu
# This some ugly/interesting tcl hacking to figure out what variables the user
# stored the node ids in.
###
rename set real_set
real_set skipset 0
proc set {args} {
    global skipset
    global nodeid_map
    global rid_map
    if {! $skipset} {
	real_set skipset 1
	real_set var [lindex $args 0]
	if {$var != "currnode" && $var != "currlan"} {
	    if {[llength $args] > 1} {
		# Munge var in case it's a variable
		regsub -all {[(]} $var {-} out
		real_set var $out
		regsub -all {[)]} $var {} out
		real_set var $out
		real_set val [lindex $args 1]
		if {([regexp {^n[0-9]+$} $val] != 0) ||
		    ([regexp {^lan[0-9]+$} $val] != 0) ||
		    ([regexp {^l[0-9]+$} $val] != 0)} {
		    # Ok, we change it so that the variable will hold
		    # it's own name.  We still need nodeid_map for
		    # classes to find their names.  rid_map is the
		    # reverse mapping of nodeid_map
		    # XXX - might be cleaner to have a class variable
		    # that we set here instead of using the nodeid_map.
		    if {([llength $args] == 2) &&
			(![info exists nodeid_map($val)])} {
			real_set nodeid_map($val) $var
			real_set rid_map($var) $val
			real_set args [list [lindex $args 0] $var]
		    }
		}
	    }
	}
	real_set skipset 0
    }
    if {[llength $args] == 1} {
	return [uplevel real_set [lindex $args 0]]
    } else {
	return [uplevel real_set [lindex $args 0] \{[lindex $args 1]\}]
    }
}
###

#nop is used for unimplemented $ns instprocs that are supposed to
#return things. the instproc returns a nop, which users call in their
#ns input file. 
proc nop {args} {}

#begin at 0. 1,2,3... i cheerfully ignore the possibility of wrapping...
set nodeID 0
set linkID 0
set lanID 0
set eventID 0

set nodelist {}

set linkslist {}

set eventlist {}

set lanlist {}

# sim.tcl handles the ns Simulator methods
source $libdir/sim.tcl

# stubs.tcl contains a lot of dummy things to allow execution of 
# ns files without going through the trouble of redoing ns or something.
# i fear that it will grow without bound (or at least until I give up and
# make this whole thing into an ns add-on and keep all of the ns behavior)

source $libdir/stubs.tcl
 
set prefix $id

# Copy in tb_compat.tcl
file copy -force $tbcompat [file dirname $nsfile]
source $nsfile
