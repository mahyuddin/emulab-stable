#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2003, 2005, 2006, 2007 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
use Getopt::Std;

sub usage()
{
    print STDOUT "Usage: rmuser [-p <pid> [-n]] uid\n" .
	"Use the -p option to remove user from a specific project\n";
    exit(-1);
}

sub fatal($);

my  $optlist = "p:n";

#
# Configure variables
#
my $TB      = "@prefix@";
my $TBOPS   = "@TBOPSEMAIL@";
my $TBLOGS  = "@TBLOGSEMAIL@";
my $CONTROL = "@USERNODE@";
my $BOSSNODE= "@BOSSNODE@";

my $MODGROUPS	= "$TB/sbin/modgroups";
my $DELACCT	= "$TB/sbin/tbacct del";

my $dbuid;
my $user;
my $query_result;
my $pidmode	= 0;
my $nuke	= 0;
my $pid;

#
# We don't want to run this script unless its the real version.
#
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be root! Maybe its a development version?\n");
}

#
# This script is setuid, so please do not run it as root. Hard to track
# what has happened.
# 
if ($UID == 0) {
    die("*** $0:\n".
	"    Please do not run this as root! Its already setuid!\n");
}

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/usr/bin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1;

# Load the Testbed support stuff. 
use lib "@prefix@/lib";
use libaudit;
use libdb;
use libtestbed;
use User;

my $HOMEDIR	= USERROOT();

#
# Check args.
#
#
# Parse command arguments. Once we return from getopts, all that should
# left are the required arguments.
#
%options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"p"})) {
    $pid     = $options{"p"};
    $pidmode = 1;

    # untaint;
    if ($pid =~ /^([-\@\w.]+)$/) {
	$pid = $1;
    }
    else {
	die("Tainted argument $pid!\n");
    }
}
if (defined($options{"n"})) {
    $nuke = 1;
}
if ($nuke && !$pidmode) {
    usage();
}
if (@ARGV != 1) {
    usage();
}
$user = $ARGV[0];

#
# Untaint the argument.
#
if ($user =~ /^(\w+)$/i) {
    $user = $1;
}
else {
    die("Tainted argument $user!\n");
}

# Map target user to object.
my $target_user = User->Lookup($user);
if (! defined($target_user)) {
    fatal("$user does not exist!");
}
my $target_dbid  = $target_user->dbid();
my $target_uid   = $target_user->uid();

# Map invoking user to object.
my $this_user = User->ThisUser();
if (! defined($this_user)) {
    fatal("You ($UID) do not exist!");
}

#
# Only TB admins are allowed to do this. At some point permit project
# leaders to delete accounts in their projects, but for now lets not.
# There are issues of people in multiple projects.
#
if (!TBAdmin()) {
    if (! defined($pid) ||
	! TBProjAccessCheck($UID, $pid, $pid, TB_PROJECT_DELUSER)) {
	die("*** $0:\n".
	    "    You do not have permission to remove user $target_user!\n");
    }
}

#
# Note hardwired control node. 
# 
my $control_node = $CONTROL;

#
# Sanity check. Must not be the head of any experiments (in the project).
#
$query_result =
    DBQueryFatal("select * from experiments ".
		 "where expt_head_uid='$target_uid' ".
		 (defined($pid) ? "and pid='$pid'" : ""));

if ($query_result->numrows) {
    die("*** $0:\n".
	"    $target_uid is still heading up experiments! Please terminate them.\n");
}

#
# Must not be the head of the project being removed from, or any projects
# if being completely removed.
#
if ($pidmode) {
    if (ProjLeader($pid) eq $target_uid) {
	die("*** $0:\n".
	    "    $target_user is the leader of project $pid!\n");
    }
}
else {
    $query_result =
	DBQueryFatal("select pid from projects where head_uid='$target_uid'");

    if ($query_result->numrows) {
	die("*** $0:\n".
	    "    $target_user is still heading up projects!\n");
    }
}

# Get current status.
$status = $target_user->status();

#
# If nuke mode is also specified, then the account is being nuked from
# web page because of a project join denial. Check to make sure user
# is not an active user (must be newuser or unapproved).
#
if ($nuke) {
    if ($status ne USERSTATUS_NEWUSER &&
	$status ne USERSTATUS_UNAPPROVED) {
	die("*** $0:\n".
	    "    $target_uid is not an unapproved user in project $pid!\n");
    }

    $query_result =
	DBQueryFatal("select pid from group_membership ".
		     "where uid_idx='$target_dbid' and pid!='$pid'");

    if ($query_result->numrows) {
	die("*** $0:\n".
	    "    $target_uid is a member of other projects!\n");
    }
}

#
# This script is always audited. Mail is sent automatically upon exit.
#
if (AuditStart(0)) {
    #
    # Parent exits normally
    #
    exit(0);
}

#
# Set the status to frozen if user being removed completely. This
# prevents possible race conditions since the user is no longer able
# to access the web interface and tmcd will no longer return account
# info for the user.
#
# In pidmode (removed from a project), hand the operation of too modgroups
# to do the rest.
#
if ($pidmode) {
    #
    # Drop root for calling modgroups.
    #
    $EUID = $UID;
    system("$MODGROUPS -r $pid:$pid $target_uid");

    exit($? >> 8)
	if (!$nuke);
    
    $EUID = 0;
}
else {
    my $args = ();

    $args{'status'} = TBDB_USERSTATUS_FROZEN;

    $target_user->Update(\%args) == 0
	or fatal("Could not update status for $target_uid!");

    #
    # Now schedule account updates; Once the user is frozen, all accounts
    # will be terminated.
    #
    TBNodeUpdateAccountsByUID($target_uid);
}

#
# Not in pidmode. Kill the user's entire group membership.
# Must be done *after* the account update!
# 
DBQueryFatal("delete from group_membership where uid_idx='$target_dbid'");

#
# Then the pubkey tables.
# 
DBQueryFatal("delete from user_pubkeys  where uid_idx='$target_dbid'");
DBQueryFatal("delete from user_sfskeys  where uid_idx='$target_dbid'");
DBQueryFatal("delete from user_sslcerts where uid_idx='$target_dbid'");

#
# Must do widearea_accounts too!
#
DBQueryFatal("delete from widearea_accounts where uid_idx='$target_dbid'");

#
# Then some other pidly tables.
# 
DBQueryFatal("delete from login where uid='$target_uid'");
DBQueryFatal("delete from nodeuidlastlogin where uid='$target_uid'");
DBQueryFatal("delete from uidnodelastlogin where uid='$target_uid'");
DBQueryFatal("delete from unixgroup_membership where uid='$target_uid'");
DBQueryFatal("delete from userslastlogin where uid='$target_uid'");
DBQueryFatal("delete from user_policies  where uid='$target_uid'");

#
# Remove user from both local and control node. No need to do this in
# nukemode (not allowed anyway) since the account never existed.
#
if (! $nuke) {
    $EUID = $UID;

    system("$DELACCT $target_uid") == 0 or
	fatal("$DELACCT $target_uid failed!");

    $EUID = 0;
}

#
# Rename the users home dir if its there.
#
if (-d "$HOMEDIR/$target_uid") {
    my $newname = "$HOMEDIR/$target_uid-" . TBDateTimeFSSafe();

    if (rename("$HOMEDIR/$target_uid", $newname)) {
	print "Renamed homedir to $newname. Remember to delete it!\n";
	
	#
	# Chown the owner/group to root and set the permissions so no
	# one is allowed to look inside.
	#
	if (! chmod(0700, $newname)) {
	    fatal("Could not chmod directory $newname to 0700: $!");
	}
	if (! chown(0, 0, $newname)) {
	    fatal("Could not chown directory $newname to 0/0: $!");
	}
    }
    else {
	fatal("Could not rename user directory to $newname: $!");
    }
}

#
# In nuke mode, we really do kill the account, since its from a denied
# project join request and there is no reason to keep the account around
# to pollute the table.
#
# Otherwise, the user is "archived" by setting his status accordingly.
#
if ($nuke) {
    DBQueryFatal("delete from users where uid_idx='$target_dbid'");
    print "User '$target_uid' has been removed!\n";
}
else {
    $target_user->SetStatus($User::USERSTATUS_ARCHIVED);
    print "User '$target_uid' has been archived!\n";
}
exit(0);

sub fatal($) {
    my($mesg) = $_[0];

    die("*** $0:\n".
	"    $mesg\n");
}
