#!/usr/bin/perl -wT
use English;

#
# Delete accounts. Note that groups are not deleted until a project is
# deleted via rmproj script.
#
# XXX - The control node is hardwired. Look for $CONTROL.
#
# usage: rmacct-ctrl <userid>
# 

#
# Configure variables
#
my $TB      = "@prefix@";
my $TBOPS   = "@TBOPSEMAIL@";
my $CONTROL = "@USERNODE@";

my $HOMEDIR = "/users";
my $SSH     = "$TB/bin/sshtb";
my $USERDEL = "/usr/sbin/pw userdel";
my $GENELISTS = "$TB/sbin/genelists";

my $user;
my $query_result;

#
# We don't want to run this script unless its the real version.
#
if ($EUID != 0) {
    die("Must be root! Maybe its a development version?");
}

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/usr/bin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1;

# Load the Testbed support stuff. 
use lib "@prefix@/lib";
use libdb;
use libtestbed;

#
# Check args.
#
if ($#ARGV < 0) {
    die("Usage: rmacct-ctrl <userid>\n");
}
$user = $ARGV[0];

#
# Untaint the argument.
#
if ($user =~ /^([a-z0-9]+)$/i) {
    $user = $1;
}
else {
    die("Invalid uid '$user' contains illegal characters.\n");
}

#
# Only TB admins are allowed to do this. At some point permit project
# leaders to delete accounts in their projects, but for now lets not.
# There are issues of people in multiple projects.
#
if ($UID && !TBAdmin($UID)) {
    die("$0: You do not have enough permission to run this script!\n");
}

#
# Note hardwired control node. 
# 
my $control_node = $CONTROL;

#
# Remove user from both local and control node. We assume FreeBSD on both.
# 
# All this stuff must be done as root (ssh).
#
$UID = $EUID;

print "Removing user $user from local node.\n";
if (system("$USERDEL $user")) {
    fatal("Could not remove user $user from local node.");
}

print "Removing user $user from $control_node.\n";
if (system("$SSH $control_node '$USERDEL $user'")) {
    fatal("Could not remove user $user from $control_node.\n");
}

#
# Rename the user directory instead of deleting it.
# 
my $newname = "$HOMEDIR/$user-" . TBDateTimeFSSafe();

if (! -e "$HOMEDIR/$user") {
    fatal("User directory '$HOMEDIR/$user' does not exist!");
}

if (! rename("$HOMEDIR/$user", $newname)) {
    fatal("Could not rename user directory $user to $newname: $!");
}

exit(0);

sub fatal {
    local($msg) = $_[0];

    SENDMAIL($TBOPS, "TESTBED: rmacct-ctrl Failed", $msg);
    die("$0: $msg");
}
