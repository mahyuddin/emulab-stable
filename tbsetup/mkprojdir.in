#!/usr/local/bin/perl -wT
use English;

#
# Make a project directory hierarchy. Must be called as tbroot.
# Creates a directory rooted /proj/pid. The directory is setuid
# to the project leader, and setgid to the project gid. We get
# this info from the database.
#
# usage: mkprojdir <pid>
#

#
# Configure variables
#
my $TB       = "@prefix@";

my $PROJROOT = "/proj";
my $TFTPROOT = "/tftpboot";
my @DIRLIST  = ("exp", "images", "logs", "deltas", "tarfiles", "rpms",
		"groups", "tiplogs");

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/usr/bin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1;

#
# Load the Testbed support stuff. 
#
use lib "@prefix@/lib";
use libdb;

#
# Check args.
#
if ($#ARGV < 0) {
    die("Usage: mkprojdir <pid>\n");
}
my $pid = $ARGV[0];

#
# Untaint the argument.
#
if ($pid =~ /^([-\@\w.]+)$/) {
    $pid = $1;
}
else {
    die("Invalid pid '$pid' contains illegal characters.\n");
}

#
# Figure out who called us. Only root or people with admin status
# in the DB can run this script.
#
if ($UID && !TBAdmin($UID)) {
    die("mkprojdir: You must be root or a TB administrator\n");
}

my $head;

#
# We need the project leader name.
#
if (! ($head = ProjLeader($pid))) {
    die("*** $0:\n".
	"    Could not get project leader for project $pid!\n");
}

#
# This acts as check (and we need the numeric uid) in case mkacct failed!
# 
my (undef,undef,$uid) = getpwnam($head)
    or die "$head not in passwd file";

my (undef,undef,$gid) = getgrnam($pid)
    or die "$pid not in group file";

#
# Okay, do it.
#
if (! mkdir("$PROJROOT/$pid", 0770)) {
    die("Could not make directory $PROJROOT/$pid: $!\n");
}

if (! chmod(0770, "$PROJROOT/$pid")) {
    die("Could not chmod directory $PROJROOT/$pid: $!\n");
}

if (! chown($uid, $gid, "$PROJROOT/$pid")) {
    die("Could not chown $PROJROOT/$pid to $uid/$gid: $!\n");
}

#
# Make project subdirs.
#
foreach my $dir (@DIRLIST) {

    if (! mkdir("$PROJROOT/$pid/$dir", 0770)) {
	die("Could not make directory $PROJROOT/$pid/$dir: $!\n");
    }
    if (! chmod(0770, "$PROJROOT/$pid/$dir")) {
	die("Could not chmod directory $PROJROOT/$pid/$dir: $!\n");
    }
    if (! chown($uid, $gid, "$PROJROOT/$pid/$dir")) {
	die("Could not chown $PROJROOT/$pid/$dir to $uid/$gid: $!\n");
    }
}

#
# Create a tftp directory for oskit kernels.
# 
if (! mkdir("$TFTPROOT/proj/$pid", 0770)) {
    die("Could not make directory $TFTPROOT/proj/$pid: $!\n");
}
if (! chmod(0777, "$TFTPROOT/proj/$pid")) {
    die("Could not chmod directory $TFTPROOT/proj/$pid: $!\n");
}
if (! chown($uid, $gid, "$TFTPROOT/proj/$pid")) {
    die("Could not chown $TFTPROOT/proj/$pid to $uid/$gid: $!\n");
}

exit(0);

