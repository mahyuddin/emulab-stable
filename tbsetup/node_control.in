#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2002, 2004 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
use Getopt::Std;

#
# usage: node_control [options] node [node ...]
#        node_control [options] -e pid,eid
#
# XXX virt_nodes osname is not handled properly.
#
# This script is invoked from ops and from the web interface. Must check
# all the args.
#
sub usage()
{
    print("Usage: node_control name=value [name=value ...] node [node ...]\n".
	  "       node_control -e pid,eid name=value [name=value ...]\n".
	  "       node_control -l\n".
	  "For multiword values, use name='word0 ... wordN'\n".
	  "Use -l to get a list of operational parameters you can change.\n".
	  "Use -e to change parameters of all nodes in an experiment.\n");
    exit(-1);
}
my  $optlist = "de:l";

#
# Array of allowed names. All of these values are text, so no need to
# worry about distinguishing numeric stuff.
#
# XXX This should be in the library.
#
my %controlset =
(
 #
 # Symbolic name =>
 #      Admin, Multi args, nodes field, virt_nodes field, osselect, checkslot
 #
 default_boot_osid      =>
     [0, 0, "def_boot_osid",      undef,       1, "",   "os_info:osid"],
 default_boot_cmdline	=> 
     [0, 0, "def_boot_cmd_line",  "cmd_line",  0, "",   "virt_nodes:cmd_line"],
 startup_command	=>
     [0, 0, "startupcmd",         "startupcmd",0, "",   "virt_nodes:startupcmd"],
 tarfiles		=>
     [0, 1, "tarballs",	          "tarfiles",  0, "",   "virt_nodes:tarfiles"],
 rpms			=>
     [0, 1, "rpms",	   	  "rpms",      0, "",   "virt_nodes:rpms"],
 next_boot_osid         =>
     [1, 0, "next_boot_osid",     undef,       1, "-1", "os_info:osid"],
 next_boot_cmdline	=>
     [1, 0, "next_boot_cmd_line", undef,       0, "",   "virt_nodes:cmd_line"],
 temp_boot_osid         =>
     [1, 0, "temp_boot_osid",     undef,       1, "-t", "os_info:osid"],
 bios_version		=>
     [1, 0, "bios_version",	  undef,       0, "",   "nodes:bios_version"],
);
  
#
# Configure variables
#
my $TB		= "@prefix@";
my $TBOPS       = "@TBOPSEMAIL@";
my $TBLOGS      = "@TBLOGSEMAIL@";

my $osselect    = "$TB/bin/os_select";
my @nodes       = ();
my %controls    = ();
my $eidmode	= 0;
my $debug       = 0;
my $errors	= 0;
my $pid;
my $eid;
my $dbuid;

#
# Load the Testbed support stuff. 
#
use lib "@prefix@/lib";
use libdb;
use libtestbed;

my $IsAdmin    = TBAdmin($UID);

# un-taint path
$ENV{'PATH'} = '/bin:/usr/bin:/usr/local/bin';
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

# Turn off line buffering on output
$| = 1; 

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
%options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"d"})) {
    $debug = 1;
}
if (defined($options{"l"})) {
    foreach my $option (keys(%controlset)) {
	my ($admin, $multi, $dbkey) = @{ $controlset{$option} };
	
	printf("  %-25s ", $option);
	if ($multi || $admin) {
	    print "- ";
	}
	if ($multi) {
	    print "(multiple options allowed) ";
	}
	if ($admin) {
	    print "(administrators only)";
	}
	print "\n";
    }
    exit(0);
}

if (defined($options{"e"})) {
    $eidmode = $options{"e"};
    if ($eidmode =~ /([-\w]*),([-\w]*)/) {
	$pid = $1;
	$eid = $2;
    }
    else {
	usage();
    }
}

if (! @ARGV) {
    usage();
}

#
# Shift off the set strings (name=value). Verify that each one is in the
# proper format.
#
while (@ARGV) {
    my $string = $ARGV[0];

    if (! ($string =~ /([-\w]*)=[\']?([^\']*)[\']?/)) {
	last;
    }
    shift;

    if (! defined($controlset{$1})) {
	die("*** $0:\n".
	    "    Illegal control setting: $1='$2'. Try the -l option!\n");
    }
    my ($admin,$multi) = @{ $controlset{$1} };

    if ($admin && ! $IsAdmin) {
	die("*** $0:\n".
	    "    You do not have permission to set $1. Try the -l option!\n");
    }

    if ($multi && defined($controls{$1})) {
	$controls{$1} = "$controls{$1}:$2";
    }
    else {
	$controls{$1} = "$2";
    }
}

if ($debug) {
    foreach my $option (keys(%controls)) {
	print "Will set $option to '$controls{$option}'\n";
    }
}

#
# In eidmode, check the access permission for the experiment, and then
# get the nodes. Otherwise, check access for each node given on the
# command line.
#
if ($eidmode) {
    # Permission check.
    if ($UID && !TBAdmin($UID) &&
	! TBExptAccessCheck($UID, $pid, $eid, TB_EXPT_MODIFY)) {
	die("*** $0:\n".
	    "    You do not have permission to control nodes in $pid/$eid!\n");
    }

    if (! (@nodes = ExpNodes($pid, $eid))) {
	die("*** $0:\n".
	    "    There are no nodes in $pid/$eid!\n");
    }
}
else {
    if (! @ARGV) {
	usage();
    }
    
    # Untaint the nodes.
    foreach my $node ( @ARGV ) {
	if ($node =~ /^([-\w]+)$/) {
	    $node = $1;
	}
	else {
	    die("Bad node name: $node.");
	}
    
	push(@nodes, $node);
    }
}

#
# Create an update clause for all of the specified values.
#
my $physnodes_updatestr;
my $virtnodes_updatestr;
my @osselect_params=();

foreach my $option (keys(%controls)) {
    my ($admin, $multi, $physdbkey, $virtdbkey,
	$needs_osselect, $osselect_arg, $checkslot) = @{ $controlset{$option} };
    my $value = $controls{$option};

    #
    # Do a checkslot on it to make sure its valid for the DB slot.
    #
    my ($table,$slot) = split(":", $checkslot);

    if ($value ne "" &&
	!TBcheck_dbslot($value, $table, $slot,
			TBDB_CHECKDBSLOT_WARN|TBDB_CHECKDBSLOT_ERROR)) {
	die("*** $0:\n".
	    "    Illegal value specified for $option: '$value'\n");
    }

    if ($needs_osselect) {
	my $str = ($debug ? "-d " : "");
	    
        if ($value eq "") {
	    # Clearing the field.
	    $str .= "-c $osselect_arg";
	}
	else {
	    # Setting the field
	    $str .= "$osselect_arg $value";
	}
	if ($debug) {
	    print "$option=$value ($physdbkey) made osselect str='$str'\n";
	}
	push(@osselect_params, $str);
    }
    else {
	if (defined($physnodes_updatestr)) {
	    $physnodes_updatestr = "$physnodes_updatestr, $physdbkey='$value'";
	}
	else {
	    $physnodes_updatestr = "$physdbkey='$value'";
	}
    }

    if (defined($virtdbkey)) {
	if (defined($virtnodes_updatestr)) {
	    $virtnodes_updatestr = "$virtnodes_updatestr, $virtdbkey='$value'";
	}
	else {
	    $virtnodes_updatestr = "$virtdbkey='$value'";
	}
    }
}
if (! defined($physnodes_updatestr) && 
    @osselect_params==0 && 
    ! defined($virtnodes_updatestr)) {
    exit(0);
}

if ($debug) {
    if (defined($physnodes_updatestr)) {
	print "Phys update will be '$physnodes_updatestr'\n";
    }
    if (@osselect_params>0) {
	print "osselect calls:\n  ".join("\n  ",@osselect_params)."\n";
    }
    if (defined($virtnodes_updatestr)) {
	print "Virt update will be '$virtnodes_updatestr'\n";
    }
}

#
# Now do it for every node. Do the permission check here to reduce the
# race condition window. Should probably lock instead, but thats a pain.
#
foreach my $node (@nodes) {
    my $node_pid;
    my $node_eid;
    my $node_vname;

    if ($debug) { print "Processing $node...\n"; }

    if (defined($physnodes_updatestr)) {
	if ($UID && !$IsAdmin &&
	    ! TBNodeAccessCheck($UID, TB_NODEACCESS_MODIFYINFO, $node)) {
	    print("*** $0:\n".
		  "    You do not have permission to modify physical ".
		  "parameters for $node!\n");
	    
	    $errors++;
	    next;
	}
	DBQueryFatal("update nodes set $physnodes_updatestr ".
		     "where node_id='$node'");
    }

    if (@osselect_params>0) {
	if ($UID && !$IsAdmin &&
	    ! TBNodeAccessCheck($UID, TB_NODEACCESS_MODIFYINFO, $node)) {
	    print("*** $0:\n".
		  "    You do not have permission to modify physical ".
		  "parameters for $node!\n");
	    
	    $errors++;
	    next;
	}
	foreach $str (@osselect_params) {
	    if ($debug) { print "Running '$osselect $str $node'\n"; }
	    if (system("$osselect $str $node")) {
		print "*** $0:\n    os_select failed!\n";
		$errors++;
	    }
	}
    }

    #
    # We need the vname for the node so that we can update the virt_nodes
    # table. This implies that we cannot update the virt_nodes unless the
    # experiment is swapped in (so we can map from phys node name to virt
    # node name). At some point, maybe we want to provide a way to change
    # the params of a swapped out experiment by having the user specify
    # the vname?
    #
    if (! NodeidToExp($node, \$node_pid, \$node_eid, \$node_vname)) {
	print("*** $0:\n".
	      "    Node $node is not a member of a swapped in experiment!\n".
	      "    Cannot set its virtual node parameters. Skipping ...\n");
	next;
    }

    if (defined($node_vname) && defined($virtnodes_updatestr)) {
	if ($UID && !$IsAdmin &&
	    ! TBExptAccessCheck($UID, $node_pid, $node_eid, TB_EXPT_MODIFY)) {
	    print("*** $0:\n".
		  "    You do not have permission to modify virtual ".
		  "parameters for $node!\n");
	    
	    $errors++;
	    next;
	}
	DBQueryFatal("update virt_nodes set $virtnodes_updatestr ".
		     "where pid='$node_pid' and eid='$node_eid' ".
		     " and vname='$node_vname'");
    }
}

exit($errors);
