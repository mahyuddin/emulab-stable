#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2007, 2010 University of Utah and the Flux Group.
# All rights reserved.
#
use strict;
use Getopt::Std;
use POSIX 'setsid';	 # For &daemonize
use POSIX ":sys_wait_h"; # For &WNOHANG
use Sys::Syslog;
use English;
use Socket;
use Errno qw(EADDRINUSE);

#
# This also kills a running frisbee.
#
sub usage()
{
    print "Usage: $0 [-d] [-k] <imageid>\n";
    print "-k:	Kill running frisbee.\n";
    print "-d:	Print debugging output.\n";
    exit(1);
}
my $optlist  = "dk";
my $debug    = 0;
my $killmode = 0;

# Configure variables
my $TB		= "@prefix@";
my $TBOPS	= "@TBOPSEMAIL@";
my $BOSSNODE_IP = "@BOSSNODE_IP@";
my $BASEADDR    = "@FRISEBEEMCASTADDR@";
my $BASEPORT    = "@FRISEBEEMCASTPORT@";
my $ELABINELAB  = @ELABINELAB@;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/usr/bin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1;

use lib "@prefix@/lib";
use libdb;
use libtestbed;
use libtblog;
use User;
use Image;

# Protos.
sub LockTables();
sub UnlockTables();
sub PickAddress();
sub SetAddress($);
sub SetPid($);
sub TestBusy();
sub ClearPid();
sub ClearAddress();
sub Fatal($);
sub debug($);

# Defines
my $FRISBEED	= "$TB/sbin/frisbeed";
my $LOGFILE	= "$TB/log/frisbeelauncher";
my $FRISBEEIMAGE= "$TB/sbin/frisbeeimage";
my $child_pid   = 0;
my $dlfilename  = 0;

my $STD_BW	= 72000000; 	# 71.6Mb/sec w/1000HZ kernel
my $USR_BW	= 54000000;	# 53.7Mb/sec w/1000HZ kernel

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
my %options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"d"})) {
    $debug = 1;
}
if (defined($options{"k"})) {
    $killmode = 1;
}
usage()
    if (! @ARGV);

#
# Verify user and get his DB uid and other info for later. Note that
# root can run this script, so we might not get an actual user.
#
my $this_user = User->ThisUser();

#
# Grab the Image.
#
my $image = Image->Lookup($ARGV[0]);
if (! defined($image)) {
    Fatal("No such image in the Emulab Database.\n");
}
my $imageid  = $image->imageid();
my $filename = $image->path();

#
# Make sure that the user has sufficient permissions.
#
if (defined($this_user) &&
    !$image->AccessCheck($this_user,
			 ($killmode ? TB_IMAGEID_DESTROY :
			  TB_IMAGEID_READINFO))) {
    Fatal("Not enough permission!");
}

if (!$killmode && !$ELABINELAB && ! -R $filename) {
    Fatal("You do not have permission to read the image file for".
	  "$image: $filename\n");
}

#
# Pick an address to use before locking the DB.  Die if unsucessful,
# set address if sucessful.  If something goes wrong later or there is
# already a laucher active, we will wind up wasting this address,
# but hey, we can make more!
#
my $naddress = PickAddress()
    if (!$killmode);

#
# Need to lock the tables here, so we can lock out anyone else from
# messing with the image.
#
LockTables();

# Try to discover if some other process is handling this address
my $address = $image->load_address();
my $pid     = $image->frisbee_pid();

if ($killmode) {
    #
    # Killmode. We do some sanity checking cause there is a small race 
    # inherent in trying to use the pid via the DB without locking (below).
    #
    # No point in leaving table locked; not going to modify it.
    #
    UnlockTables();

    # Nothing running.
    exit(0)
	if (!$address && !$pid);

    if (!$address && $pid) {
	#
	# This makes no sense. Just send email.
	#
	my $mesg = "Inconsistent DB state. PID ($pid) but no load address!";
	
	SENDMAIL($TBOPS,
		 "Frisbee Killer Failed!",
		 "Image: $image\n".
		 $mesg);
	
	Fatal($mesg);
    }
    if ($address && !$pid) {
	#
	# Okay, minor problem. It is possible we caught the launcher between
	# setting the address and setting the pid. Wait a moment, and then
	# try again. If still no pid, bail.
	#
	sleep(1);
	$image->Refresh();

	$address = $image->load_address();
	$pid     = $image->frisbee_pid();

	# Okay, situation resolved itself; other frisbeelauncher bailed.
	exit(0)
	    if (!$address && !$pid);

	#
	# Still inconsistent so bail.
	# 
	if ($address && !$pid) {
	    my $mesg = "Inconsistent DB state. Load address but no PID!";
	
	    SENDMAIL($TBOPS,
		     "Frisbee Killer Failed!",
		     "Image: $image\n".
		     $mesg);
	
	    Fatal($mesg);
	}
    }

    #
    # Okay, address and pid. We could clear the pid from the DB,
    # preventing another killer from thinking it is running, but not
    # much point since it is not likely to happen. If it turns out to
    # be a problem we can change the way this works.
    #
    if (! kill('TERM', $pid)) {
	SENDMAIL($TBOPS,
		 "Frisbee Killer Failed!",
		 "Failed to stop frisbee daemon for $image\n".
		 "Could not kill(TERM) process $pid: $? $!");

	Fatal("Failed to stop frisbee daemon for $image!");
    }
    exit(0);
}

#
# Keep the current frisbeed running for another time period.
#
if ($address) {
    $image->KeepBusy();
    UnlockTables();
    debug("A server ($address) is already running for image $image\n");
    exit(0);
}

# This would be inconsistent.
if ($pid) {
    my $mesg = "Inconsistent DB state. No load address but PID ($pid) set!";
	
    UnlockTables();

    SENDMAIL($TBOPS,
	     "Frisbee Startup Failed!",
	     "Image: $image\n".
	     $mesg);
	
    Fatal($mesg);
}

#
# Ok, no process is already running for this image, we will use the new
# address chosen earlier.
#
$address = $naddress;
SetAddress($address);
UnlockTables();
debug("Picked address $address\n");

#
# When running inside an inner Emulab, try to get the image from the
# outer emulab when it does not exist locally. Of course, it could still
# be a bogus image.
#
# We do this before going into the background as that is what os_setup
# expects.  Note that we set a signal handler so we can remove any partially
# downloaded image if interrupted.  We also record our pid so that -k will
# work in another frisbeelauncher instance.
#
if ($ELABINELAB && ! -e $filename) {
    debug("Fetching image $filename ($imageid) from real boss\n");
    $SIG{HUP} = $SIG{INT} = $SIG{TERM} = \&cleanup;
    SetPid($PID);
    $EUID = $UID;
    $dlfilename = $filename;
    system("$FRISBEEIMAGE $imageid");
    $dlfilename = 0;
    $EUID = 0;
    if ($?) {
	Fatal("No such image file: $filename!");
    }
}

# Run in the background
if (my $childpid = TBBackGround($LOGFILE)) {
    #
    # Delay a moment, and they look for an exit status.
    # This is intended to catch startup problems.
    #
    # XXX TBBackGround should do this!
    #
    sleep(1);
    my $foo = waitpid($childpid, &WNOHANG);
    if ($foo) {
	LockTables();
	ClearAddress();
	UnlockTables();
	Fatal("Error $? backgrounding frisbeelauncher!");
    }
    exit(0);
}
TBdbfork();

# Set up a signal handler that will clean up in case we get killed
$SIG{HUP} = $SIG{INT} = $SIG{TERM} = \&cleanup;

# Set our pid. This happens outside the lock which could lead to races,
# but that is unlikely. Look for it above though.
SetPid($PID);

#
# Drop root permissions, if we have them
#
if ($EUID == 0) {
    $EUID = $UID;
}

my $args = "-i $BOSSNODE_IP";

#
# Select the appropriate bandwidth
#
if ($filename =~ /^$TB\/images/ && !$ELABINELAB) {
    $args .= " -W $STD_BW";
} else {
    $args .= " -W $USR_BW";
}

#
# Force multicast keepalives if necessary
#
if ($ELABINELAB) {
    $args .= " -K 15";
}

my $firsttry = 1;

# Now, we actually launch Frisbee
while (1) {
        #
        # Each time the server exits, test the busy bit to see if
	# it should keep going. This has to be done with tables locked
	# since another caller is going to bump it.
	#
    	LockTables();
	last
	    if (! TestBusy());
	UnlockTables();
	
	if ($child_pid = fork()) {
		# Wait for child to exit
		waitpid($child_pid, 0);

		if ($?) {
		    #
		    # If this is the first time we are firing up this server
		    # and the port is already in use, try another port.
		    #
		    # If a server was running, died, and now we can no longer 
		    # get the port, something else might be wrong so just die
		    # in the usual way.
		    #
		    my $err = $?;
		    if ($firsttry && ($err >> 8) == EADDRINUSE()) {
			warn("Frisbeed bind failed for address $address, ".
			     "picking another address\n");
			$address = PickAddress();
			LockTables();
			SetAddress($address);
			UnlockTables();
			next;
		    }
		    SENDMAIL($TBOPS, "Frisbeed Failed!",
			     "Image: $image\n".
			     "Address: $address\n\n".
			     "Process $child_pid exited with value $err.\n".
			     "Please look at the syslog for frisbeed!\n\n".
			     "NOTE: Another frisbeed will not start!\n");
		    #
		    # Dump early. This will leave the address in
		    # in the DB, so that another one will not start
		    # until the matter is resolved by someone. 
		    #
		    ClearPid();
		    exit(1);
		}
		else {
		    $firsttry = 0;
		}
	}
	else {
		# Child branch
		# The database format for address is host:port - however,
		# we need to give them as seperate arguments to frisbeed.
		if ($address =~ /(.*):(.*)/) {
		    my $addr = $1;
		    my $port = $2;

		    if (!exec("$FRISBEED $args -m $addr -p $port $filename")) {
			Fatal("$$: Unable to exec $FRISBEED");
		    }
		}
		Fatal("$$: Bad address format: $address!");
	}
}
ClearAddress();
UnlockTables();
exit(0);

######################################################################
# Subroutines
######################################################################

# Only print if -d option was given. Also add $$ on the beginning of the
# string, to ease debugging
sub debug($)
{
    my ($msg) = @_;

    print "$$: $msg"
	if ($debug);
}

# Lock the tables used in this script
sub LockTables()
{
    debug("Locking tables\n");
    if ($image->LockTables("images write") != 0) {
	Fatal("Error locking tables");
    }
}

# Unlock the tables used in this script
sub UnlockTables()
{
    $image->UnLockTables();
    debug("Unlocked tables\n");
}

#
# One-time conversion function.
# Convert overly complex frisbee_mcastaddr index into simple incremented one.
#
sub CreateIndex()
{
    my $idx = 0;

    DBQueryFatal("lock tables emulab_indicies write");

    my $baseaddr_query =
	DBQueryFatal("select idx from emulab_indicies ".
		     "where name='frisbee_mcastaddr'");
    if ($baseaddr_query->num_rows) {
	($idx) = $baseaddr_query->fetchrow_array();
	my $orig = unpack("N", inet_aton("${BASEADDR}.1"));
	$idx -= $orig;
	# absolutely anal: we didn't use 2 of every 256 addresses
	my $tweak = int($idx / 256) * 2;
	$idx -= $tweak;
	if ($idx < 0 || $idx > 1000000) {
	    $idx = 0;
	}
    }
    $idx++;

    DBQueryFatal("insert into emulab_indicies (name, idx) ".
		 " values ('frisbee_index', $idx)");

    DBQueryFatal("unlock tables");
    return $idx;
}

#
# Pick out an address to use.
# DB tables must be *unlocked* for this to work!
#
sub PickAddress()
{
    my $idx;
    my $index_query =
	DBQueryFatal("update emulab_indicies set idx=LAST_INSERT_ID(idx+1) ".
		     "where name='frisbee_index'");

    if ($index_query->affectedrows <= 0) {
	my $msg = "Doing one-time creation of frisbee_index in emulab_indicies table";
	SENDMAIL($TBOPS,
		 "FrisbeeLauncher Notice!",
		 "Image: $image\n".
		 $msg);

	$idx = CreateIndex();
    } else {
	$idx = $index_query->insertid();
    }

    my ($a,$b,$c,$d) = split /\./, $BASEADDR;
    $b = 1 if (!defined($b));
    $c = 1 if (!defined($c));
    $d = 1 if (!defined($d));

    $d += $idx;
    if ($d > 254) {
	$c += int($d / 254);
	$d = $d % 254 + 1;
    }
    if ($c > 254) {
	$b += int($c / 254);
	$c = $c % 254 + 1;
    }
    if ($b > 254) {
	my $mesg = "No more multicast addresses!";

	SENDMAIL($TBOPS,
		 "FrisbeeLauncher Failed!",
		 "Image: $image\n".
		 $mesg);

	Fatal($mesg);
    }

    my $address = "${a}.${b}.${c}.${d}";
    my $port    = $BASEPORT + ((($c << 8) | $d) & 0x7FFF);
    
    return "${address}:${port}";
}

#
# Set the load address and busy bit.
# Always called with tables locked.
#
sub SetAddress($)
{
    my ($address) = @_;

    $image->Update({"load_address" => $address, "load_busy" => 1}) == 0 or
	Fatal("Could not update load address for $image");
}

#
# Clear out the address (and pid) registered to this process
# Might be called with tables locked.
#
sub ClearAddress()
{
    debug("Clearing out registered load_address and pid\n");

    $image->Update({"load_address" => '',
		    "load_busy"    => 0,
		    "frisbee_pid"  => 0}) == 0 or
	Fatal("Could not clear load address for $image");
}

#
# Set and clear the frisbee process ID.
# Always called with tables unlocked.
#
sub SetPid($)
{
    my ($pid) = @_;

    $image->Update({"frisbee_pid" => $pid}) == 0 or
	Fatal("Could not update load address for $image");
}
sub ClearPid()
{
    SetPid(0);
}

#
# Test the busy indicator, and set to zero.
# Always called with tables locked.
#
sub TestBusy()
{
    my $busy = $image->load_busy();

    if ($busy) {
	if ($image->Update({"load_busy" => 0}) != 0) {
	    UnlockTables();
	    Fatal("Could not clear busy for $image");
	}
    }	
    return $busy;
}

#
# Kill off our child process, if started, and clear out registered address
# Also, die off
#
sub cleanup
{
	print STDERR "$$: Killed, cleaning up\n";
	if ($dlfilename) {
	    $SIG{HUP} = $SIG{INT} = $SIG{TERM} = 'IGNORE';
	    kill('TERM', 0);
	    $dlfilename = 0;
	}
	if ($child_pid) {
	    kill(15, $child_pid);
	}
	ClearAddress();
	exit(1);
}

sub Fatal($)
{
    my ($msg) = @_;
    
    tbdie("*** $0:\n".
	  "    $msg\n");
}
