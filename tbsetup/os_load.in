#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2005 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
use Getopt::Std;

#
# Load an image onto a disk. The image must be in the DB images table,
# which defines how/where to load, and what partitions are affected.
# The nodes and partitions tables are updated appropriately.
#
sub usage()
{
    print("Usage: os_load [-s] [[-p <pid>] -i <imageid>] <node> [node ...]\n".
	  "       os_load [-s] [[-p <pid>] -i <imageid>] -e pid,eid\n".
	  "       os_load -l\n".
	  "Use -i to specify an image ID. Use node default otherwise.\n".
	  "Use -p to specify the project ID in which to find the imageid.\n".
	  "Use -m to specify the internal name if an image ID.\n".
	  "Use -s to start reload, but do not wait for it to complete.\n".
	  "Use -w to wait for the nodes to finish booting.\n".
	  "Use -r to supress rebooting nodes - you'll need to to it yourself\n".
	  "Use -e to reload all the nodes in an experiment.\n" .
	  "Use -l to get a list of images you are permitted to load.\n".
	  "Use -z <style> to zero all unallocated blocks on the disk\n".
	  "       style==0: don't zero (same as not using -z)\n".
	  "       style==1: let frisbee do the zeroing\n".
	  "       style==2: zero disk before running frisbee\n");
    exit(-1);
}
my $optlist   = "swldi:e:p:m:rz:";
my $waitmode  = 1;
my $listonly  = 0;
my $debug     = 0;
my $noreboot  = 0;
my $zerofree  = 0;
my @nodes     = ();
my $imagepid;
my $imagename;
my $imageid;
my $pid;
my $eid;

# Configure variables
my $TB		= "@prefix@";

# Load the Testbed support stuff.
use lib "@prefix@/lib";
use libdb;
use libosload;
use libtestbed;

# Be careful not to exit on transient error
$libdb::DBQUERY_MAXTRIES = 30;

# un-taint path
$ENV{'PATH'} = '/bin:/usr/bin:/usr/local/bin';
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

# Turn off line buffering on output
$| = 1;

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
my %options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
$debug = 1
    if (defined($options{"d"}));

# List only mode. No need to do anymore argument processing.
sub dolisting();
if (defined($options{"l"})) {
    dolisting();
    exit(0);
}

$waitmode = 0
    if (defined($options{"s"}));
$waitmode = 2
    if (defined($options{"w"}));
$noreboot = 1
    if (defined($options{"r"}));
$zerofree = $options{"z"}
    if (defined($options{"z"}));

#
# Figure out which nodes. Choice of nodes on command line, or all nodes in an
# experiment. To get all free nodes, must use sched_reload. 
# 
if (defined($options{"e"})) {
    usage()
	if (@ARGV);
    
    my $pideid = $options{"e"};

    if ($pideid =~ /([-\w]*),([-\w]*)/) {
	$pid = $1;
	$eid = $2;
	if (! (@nodes = ExpNodes($pid, $eid))) {
	    die("*** $0:\n".
		"    There are no nodes in $pid/$eid!\n");
	}
    }
    else {
	die("*** $0:\n".
	    "    Invalid argument to -e option: $pideid\n");
	usage();
    }
}
else {
    usage()
	if (! @ARGV);
    
    # Untaint nodes.
    foreach my $node (@ARGV) {
	if ($node =~ /^([-\w]+)$/) {
	    $node = $1;
	}
	else {
	    die("*** Bad node name: $node.\n");
	}
	push(@nodes, $node);
    }
}

#
# Image name (user visible name, not the internal ID). User is allowed
# to specify the pid the belongs to, but this is mostly broken cause
# images are stored in proj trees and created via NFS, so not really
# possibly to share images between projects; must be a member of the
# project. Could be fixed.
# 
if (defined($options{"i"})) {
    usage()
	if (defined($options{"m"}));
    
    $imagename = $options{"i"};
    
    if ($imagename =~ /^([-\w\.\+]+)$/) {
	$imagename = $1;
    }
    else {
	die("*** Bad data in imagename: $imagename.\n");
    }

    if (defined($options{"p"})) {
	$imagepid = $options{"p"};
	
	if ($imagepid =~ /^([-\w\.\+]+)$/) {
	    $imagepid = $1;
	}
	else {
	    die("*** Bad data in imagepid: $imagepid.\n");
	}
    }

    #
    # If -p option given, use that.
    # If in experiment mode, then use the pid of the experiment, unless
    # a -p option was given.
    # Otherwise look in the system project.
    #
    if (defined($imagepid)) {
	if (! ($imageid = TBImageID($imagepid, $imagename))) {
	    die("*** $0:\n".
		"    No such image $imagename in project $imagepid!\n");
	}
    }
    else {
	if (defined($pid)) {
	    $imageid = TBImageID($pid, $imagename);
	}
	if (! $imageid) {
	    $imageid = TBImageID(TB_OPSPID(), $imagename);
	}
	if (! $imageid) {
	    die("*** $0:\n".
		"    No such image $imagename!\n");
	}
    }
}

#
# Or an internal imageID. Sometimes easier.
# 
if (defined($options{"m"})) {
    usage()
	if (defined($options{"i"}));

    $imageid = $options{"m"};
    
    if ($imageid =~ /^([-\w\.\+]+)$/) {
	$imageid = $1;
    }
    else {
	die("*** Bad data in imageid: $imageid\n");
    }
}

#
# Weed out non-imageable nodes (e.g., virtnodes, emotes, etc.)
#
my $first = 1;
my @temp = ();
foreach my $node ( @nodes ) {
    if (!TBIsNodeImageable($node)) {
	#
	# Common mistake: forget the -i before the imagename, e.g.,
	# "os_load FBSD54-STD pcNN", which results in pcNN getting loaded
	# with the default image.  So if the first arg fails as a node, but
	# is an image ID, assume they have made this mistake and stop.
	#
	my $_pid = defined($imagepid) ? $imagepid : TB_OPSPID();
	if ($first && !defined($imagename) && TBImageID($_pid, $node)) {
	    print "*** reload: forgot the -i before image name $node?\n";
	    exit(1);
	}
	print "*** reload ($node): cannot image node, skipped.\n";
	$first = 0;
	next;
    }
    push(@temp, $node);
    $first = 0;
}
@nodes = @temp;
if (! @nodes) {
    print "*** reload: No nodes to load. Exiting.\n";
    exit(0);
}

#
# Okay, call into the library using a hash of arguments. Pass a reference
# to the args array, and to a return parameter for the list of failed nodes.
#
# NB: Permission checking is done in the library. Maybe that is wrong?
#
my %osloadargs  = ();
my %failednodes = ();

$osloadargs{'debug'}    = $debug;
$osloadargs{'waitmode'} = $waitmode;
$osloadargs{'noreboot'} = $noreboot;
$osloadargs{'zerofree'} = $zerofree;
$osloadargs{'nodelist'} = [ @nodes ];
# No imageid means to load the default image.
$osloadargs{'imageid'}  = $imageid
    if (defined($imageid));
$osloadargs{'swapinfo'} = 1;

exit(osload(\%osloadargs, \%failednodes));

# Print a listing of imageids.
sub dolisting() {
    my($query_result);

    if ($UID && !TBAdmin($UID)) {
	my ($me) = getpwuid($UID);
	$query_result =
	    DBQueryFatal("select distinct i.* from images as i ".
			 "left join group_membership as g on g.pid=i.pid ".
			 "where g.uid='$me' or i.global ".
			 "order by i.pid,i.imageid");
    } else {
	$query_result =
	    DBQueryFatal("SELECT * FROM images order by imageid");
    }

    if ($query_result->numrows) {
	printf "%-12s %-20s %s\n", "Pid", "Imagename", "Description";
	printf "------------ -------------------- -------------------------\n";

	for ($i = 0; $i < $query_result->numrows; $i++) {
	    my %row  = $query_result->fetchhash();
	    my $id   = $row{'imageid'};
	    my $pid  = $row{'pid'};
	    my $name = $row{'imagename'};
	    my $desc = $row{'description'};

	    printf "%-12s %-20s %s\n", $pid, $name, $desc;
	}
    }
}
