#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2002 University of Utah and the Flux Group.
# All rights reserved.
#

use English;

#
# Untaint path
#
$ENV{'PATH'} = '/bin:/sbin:/usr/bin:/usr/local/bin:/etc/rc.d/testbed';
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Prototypes.
#
sub fatal($);
sub background();

#
# Turn off line buffering on output
#
$| = 1;

#
# Load the OS independent support library. It will load the OS dependent
# library and initialize itself. 
# 
use lib "/etc/rc.d/testbed";
use libsetup;

#
# Get the config parameters so we can open up a log file in the proper
# location. See below.
#
my ($pid, $eid, $vname);
if (! (($pid, $eid, $vname) = check_status())) {
    fatal("Could not determine pid/eid");
}

#
# Disconnect from the caller (at daemon).
#
if (background()) {
    #
    # Parent exits normally
    #
    exit 0;
}

#
# Open up the command file, which tells us what to do.
#
open(CMD, TMSTARTUPCMD) or
    fatal("Could not open " . TMSTARTUPCMD . ": $!");

my $runcmd;
my $login;

while (<CMD>) {
    if ($_ =~ /CMD=\'([-\@\w.\/ ]+)\' UID=([0-9a-z]+)/) {
	$runcmd = $1;
	$login  = $2;
    }
}

if (!defined($runcmd) || !defined($login)) {
    fatal(TMSTARTUPCMD . " not in proper format!");
}
(undef,undef,$uid,$gid,undef,undef,undef,undef) = getpwnam($login) or
    fatal("Could not determine UID for $login");

chown($uid, $gid, $logname) or
    fatal("Could not chown $logname to $uid/$gid: $!\n");

#
# Fork a child so that we can run the command as the user and wait for it.
# 
$mypid = fork();
if ($mypid) {
    #
    # Parent waits for child.
    #
    waitpid($mypid, 0);

    $stat = $? >> 8;
}
else {
    #
    # The child becomes the user and runs the command.
    #
    $EGID = $GID = $gid;
    $EUID = $UID = $uid;

    exec($runcmd) or
	fatal("Could not exec $runcmd: $!");
}

print STDOUT "$runcmd returned $stat\n";

#
# Use the TMCC to tell the TMCD what the exit status was.
#
startcmdstatus($stat);
exit(0);

sub fatal($)
{
    my($mesg) = $_[0];

    print STDOUT "$mesg\n";

    #
    # Use the TMCC to tell the TMCD that we screwed the pooch.
    #
    startcmdstatus(666);
    exit(-1);
}

#
# Put ourselves into the background so that caller sees immediate response.
# 
sub background()
{
    # Turn off line buffering on output
    #
    $| = 1;

    $mypid = fork();
    if ($mypid) {
	return $mypid;
    }

    #
    # We have to disconnect from the caller by redirecting both STDIN and
    # STDOUT away from the pipe. Otherwise the caller (the web server) will
    # continue to wait even though the parent has exited. 
    #
    open(STDIN, "< /dev/null") or
	die("opening /dev/null for STDIN: $!");

    #
    # Create a temporary name for a log file and untaint it.
    #
    $logname = `mktemp /proj/$pid/logs/runlog-$eid.$vname.XXXXXX`;
    if ($?) {
	die("mktemp failed!");
    }

    # Note different taint check (allow /).
    if ($logname =~ /^([-\@\w.\/]+)$/) {
	$logname = $1;
    } else {
	die "Bad data in $logname";
    }

    chmod(0644, $logname);
    open(STDERR, ">> $logname") or die("opening $logname for STDERR: $!");
    open(STDOUT, ">> $logname") or die("opening $logname for STDOUT: $!");

    return 0;
}
