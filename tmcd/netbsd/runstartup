#!/usr/pkg/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2002 University of Utah and the Flux Group.
# All rights reserved.
#

use English;

#
# We are run out of the at daemon as root.
#
my $TMCC	 = "/etc/testbed/tmcc";
my $TMSTARTUPCMD = "/etc/testbed/startupcmd";
my $logname;

#
# Load the testbed library.
#
push(@INC, "/etc/testbed");
require setuplib;

#
# Untaint path
#
$ENV{'PATH'} = '/bin:/sbin:/usr/bin:/usr/local/bin:/etc/testbed';
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Get the config parameters so we can open up a log file in the proper
# location. See below.
#
my ($pid, $eid, $vname) = check_status();
if (! defined($pid)) {
    fatal("Could not determine pid/eid");
}

#
# Disconnect from the caller (at daemon).
#
if (background()) {
    #
    # Parent exits normally
    #
    exit 0;
}

#
# Open up the command file, which tells us what to do.
#
open(CMD, "$TMSTARTUPCMD") or
    fatal("Could not open $TMSTARTUPCMD: $!");

my $runcmd;
my $login;

while (<CMD>) {
    if ($_ =~ /CMD=\'([-\@\w.\/ ]+)\' UID=([0-9a-z]+)/) {
	$runcmd = $1;
	$login  = $2;
    }
}

if (!defined($runcmd) || !defined($login)) {
    fatal("$TMSTARTUPCMD not in proper format!");
}
(undef,undef,$uid,$gid,undef,undef,undef,undef) = getpwnam($login) or
    fatal("Could not determine UID for $login");

chown($uid, $gid, $logname) or
    fatal("Could not chown $logname to $uid/$gid: $!\n");

#
# Fork a child so that we can run the command as the user and wait for it.
# 
$mypid = fork();
if ($mypid) {
    #
    # Parent waits for child.
    #
    waitpid($mypid, 0);

    $stat = $? >> 8;
}
else {
    #
    # The child becomes the user and runs the command.
    #
    $EGID = $GID = $gid;
    $EUID = $UID = $uid;

    exec($runcmd);
    fatal("Could not exec $runcmd");
}

print STDOUT "$runcmd returned $stat\n";

#
# Use the TMCC to tell the TMCD what the exit status was.
#
system("$TMCC startstatus $stat");

exit(0);

sub fatal($)
{
    my($mesg) = $_[0];

    print STDOUT "$mesg\n";

    #
    # Use the TMCC to tell the TMCD that we screwed the pooch.
    #
    system("$TMCC startstatus 666");

    exit(-1);
}

#
# Put ourselves into the background so that caller sees immediate response.
# 
sub background()
{
    # Turn off line buffering on output
    #
    $| = 1;

    $mypid = fork();
    if ($mypid) {
	return $mypid;
    }

    #
    # We have to disconnect from the caller by redirecting both STDIN and
    # STDOUT away from the pipe. Otherwise the caller (the web server) will
    # continue to wait even though the parent has exited. 
    #
    open(STDIN, "< /dev/null") or
	die("opening /dev/null for STDIN: $!");

    #
    # Create a temporary name for a log file and untaint it.
    #
    $logname = `mktemp /proj/$pid/logs/runlog-$eid.$vname.XXXXXX`;
    if ($?) {
	die("mktemp failed!");
    }

    # Note different taint check (allow /).
    if ($logname =~ /^([-\@\w.\/]+)$/) {
	$logname = $1;
    } else {
	die "Bad data in $logname";
    }

    chmod(0644, $logname);
    open(STDERR, ">> $logname") or die("opening $logname for STDERR: $!");
    open(STDOUT, ">> $logname") or die("opening $logname for STDOUT: $!");


    return 0;
}
