#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2004 University of Utah and the Flux Group.
# All rights reserved.
#
use strict;
use Getopt::Std;
use English;

#
# Wrapper for running the linktest daemon. This script is currently
# setup so it can run on either boss (or ops via plasticwrap), or from
# an experimental node. Hence the odd paths and ltevent aux program,
# which is not really on boss. Also not use of tmcc to find the server. 
#
sub usage()
{
    print "Usage: run_linktest.pl ".
	  "[-q] [-d] [-s server] [-p port] [-k keyfile] [-l level] [-o logfile] -e pid/eid\n".
	  "Use -q for quick termination mode, same as -l 3\n";
    exit(1);
}
my $optlist = "qd:s:p:k:e:l:o:";
my $debug   = 0;
my $server;
my $keyfile;
my $port;
my $pid;
my $eid;
my $logfile;
my $startAt = 1; # default start level
my $stopAt = 4 ; # default stop level

# Local goo
my $TB          = "@prefix@";
my $TMCC	= "@CLIENT_BINDIR@/tmcc";
my $LTEVENT     = "@CLIENT_BINDIR@/ltevent";
my $LTEVENTBOSS = "$TB/libexec/ltevent";
my $BOSSNODE    = "@BOSSNODE@";

#
# This script should be run as a real person!
#
if (! $EUID) {
    die("*** $0:\n".
	"    This script should not be run as root!\n");
}

# un-taint path
$ENV{'PATH'} = '/bin:/usr/bin:/usr/local/bin';
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

$| = 1; #Turn off line buffering on output

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
my %options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (@ARGV) {
    usage();
}
if (defined($options{"d"})) {
    $debug = $options{"d"};
    if ($debug =~ /^([\w]+)$/) {
	$debug = $1;
    }
    else {
	die("*** $0:\n".
	    "    Bad data in debug: $debug\n");
    }
}
if (defined($options{"l"})) {
    $stopAt = $options{"l"};
    if ($stopAt =~ /^(\d)$/) {
	$stopAt = $1;
    }
    else {
	die("*** $0:\n".
	    "    Bad data in level: $stopAt\n");
    }
}

if (defined($options{"q"})) {
    # ignore if via -l they are already in quick mode.
    if($stopAt > 3) { 
	$stopAt = 3; 
    }
}
if (defined($options{"s"})) {
    $server = $options{"s"};
    if ($server =~ /^([-\w\.]+)$/) {
	$server = $1;
    }
    else {
	die("*** $0:\n".
	    "    Bad data in server: $server\n");
    }
}
if (defined($options{"k"})) {
    $keyfile = $options{"k"};
    if ($keyfile =~ /^([-\w\.\/]+)$/) {
	$keyfile = $1;
    }
    else {
	die("*** $0:\n".
	    "    Bad data in keyfile: $keyfile\n");
    }
}
if (defined($options{"o"})) {
    $logfile = $options{"o"};
    if ($logfile =~ /^([-\w\.\/]+)$/) {
	$logfile = $1;
    }
    else {
	die("*** $0:\n".
	    "    Bad data in logfile: $logfile\n");
    }
}
if (defined($options{"p"})) {
    $port = $options{"p"};
    if ($port =~ /^(\d+)$/) {
	$port = $1;
    }
    else {
	die("*** $0:\n".
	    "    Bad data in port: $port\n");
    }
}
if (defined($options{"e"})) {
    ($pid,$eid) = split(/\//, $options{"e"});
}
else {
    usage();
}

#
# Untaint args.
#
if ($pid =~ /^([-\w]+)$/) {
    $pid = $1;
}
else {
    die("*** $0:\n".
	"    Bad data in pid: $pid\n");
}
if ($eid =~ /^([-\@\w]+)$/) {
    $eid = $1;
}
else {
    die("*** $0:\n".
	"    Bad data in eid: $eid\n");
}

# signal handler in case the process is killed.
$SIG{INT} = sub {
    print "Aborted. Linktest continues on nodes.\nErrors so far:\n";
    exit &analyze;
};

#
# Need to figure out the elvind server. Since this script runs on boss
# and on experimental nodes, do something gross!
#
if (!defined($server)) {
    if (-x $TMCC) {
	my $results = `$TMCC bossinfo`;
	if ($results =~ /^([\w\.]*)\s/) {
	    $server = $1; 
	}
	else {
	    die("*** $0:\n".
		"    Bad data in server: $results\n");
	}
    }
    else {
	# We can do this on boss/ops, but not on experimental node.
	$server = $BOSSNODE;
    }
}

#
# These days, must use a keyfile! Hmm, linktest.c is not using a keyfile.
#
if (!defined($keyfile)) {
    $keyfile = "/proj/$pid/exp/$eid/tbdata/eventkey";
}

my $linktest_path; # path to linktest data.
$linktest_path = "/proj/" . $pid . "/exp/" . $eid . "/tbdata/linktest";

# send the startup event.
my $args = starter();
# event arguments
$args .=  " -x START";
$args .= " STARTAT=$startAt STOPAT=$stopAt";
$args .= " DEBUG=$debug"
    if ($debug);

system($args);
if ($?) {
    die("*** $0:\n".
	"    Error running '$args'\n");
}

print "Linktest in progress...\n";
print "Quick termination requested.\n"
    if (defined($options{"q"}));
print "Debug mode requested.\n"
    if ($debug);

# wait for the shutdown event.
$args = starter();
$args .= " -w STOP";

system($args);
if ($?) {
    die("*** $0:\n".
	"    Error running '$args'\n");
}

#
# Spit out the results?
# 
my @dir_contents;
opendir(DIR, $linktest_path) ||
    die("*** $0:\n".
	"    Cannot open $linktest_path\n");
@dir_contents = grep(/\.fatal$|\.error$/, readdir(DIR));
closedir(DIR);

foreach my $file (@dir_contents) {
    # Hmm, need to taint check the filenames. Ick.
    if ($file =~ /^([-\w\.\/]+)$/) {
	$file = $1;
    }
    else {
	die("*** $0:\n".
	    "    Bad data in filename: $file\n");
    }
    if(defined($options{"o"})) {
	open LOG_FILE, ">>$logfile" || die "Could not open $logfile for append: $!";
	open NODE_TRACE, "$linktest_path/$file" || die "Could not open $file for read: $!";
	while(<NODE_TRACE>) {
	    print LOG_FILE $_;
	}
	close NODE_TRACE;
	close LOG_FILE;
    } else {
	system("/bin/cat $linktest_path/$file");
    }
}
exit(scalar(@dir_contents));

# Initial part of command string to ltevent.
sub starter {
    my $cmd;

    if (-x $LTEVENTBOSS) {
	$cmd = $LTEVENTBOSS;
    }
    else {
	$cmd = $LTEVENT;
    }
    $cmd .= " -s $server -e $pid/$eid";
    $cmd .= " -p $port"
	if (defined($port));
    $cmd .= " -k $keyfile"
	if (defined($keyfile));
    
    return $cmd
}
