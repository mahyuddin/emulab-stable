<?xml version="1.0" encoding="utf-8"?>
<display:DefaultWindow
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:protogeniflash="protogeniflash.*" initialize="init()"
	title="Slice" xmlns:display="protogeni.display.*">
	
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.collections.Sort;
			import mx.collections.SortField;
			import mx.containers.TabNavigator;
			import mx.controls.Alert;
			import mx.controls.TextArea;
			import mx.controls.scrollClasses.ScrollThumb;
			import mx.core.DragSource;
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			
			import protogeni.ProtogeniEvent;
			import protogeni.ProtogeniHandler;
			import protogeni.resources.ComponentManager;
			import protogeni.resources.PhysicalNode;
			import protogeni.resources.Slice;
			import protogeni.resources.Sliver;
			import protogeni.resources.VirtualNode;
			
			[Bindable]
			private var cms:Array;
			private var cmsSrc:Array;
			
			[Bindable]
			public var nodes:ArrayCollection;
			
			private var cm:ComponentManager;
			
			public function init():void
			{
				Main.protogeniHandler.addEventListener(ProtogeniEvent.COMPONENTMANAGER_CHANGED, cmChanged);
				Main.protogeniHandler.addEventListener(ProtogeniEvent.SLICE_CHANGED, sliceChanged);
			}
			
			public function sliceChanged(e:ProtogeniEvent):void
			{
				if(e.changedObject == sliceCanvas.slice)
					refreshSlice();
			}
			
			public function cmChanged(e:ProtogeniEvent):void
			{
				if(e.changedObject == cm)
					loadNodes();
			}
			
			public function loadNodes():void {
				if(cms == null)
					return;
				cm = cmsSrc[selectCm.selectedIndex];
				if(this.hideUnavailableCheckBox.selected)
				{
					nodes = cm.getAvailableNodes();
					for each(var sn:SliceNode in this.sliceCanvas.allNodes)
					{
						if(!sn.node.isVirtual &&
							sn.node.manager == cm &&
							nodes.getItemIndex(sn.node.physicalNode) > -1)
							nodes.removeItemAt(nodes.getItemIndex(sn.node.physicalNode));
					}
				}
				else
					nodes = cm.AllNodes;
				
				var nameSort:Sort = new Sort();
				var dataSortField:SortField = new SortField();
				dataSortField.name = "name";
				nameSort.fields = [dataSortField];
				
				nodes.sort = nameSort;
				nodes.refresh();
			}
			
			public function refreshSlice():void
			{
				sliceCanvas.refreshSlice();
			}
			
			public function loadSlice(s:Slice):void
			{
				sliceCanvas.load(s);
				
				cms = new Array();
				cmsSrc = new Array();
				for each(var cm:ComponentManager in Main.protogeniHandler.ComponentManagers)
				{
					if(cm.Status == ComponentManager.VALID)
					{
						cmsSrc.push(cm);
						cms.push(cm.Hrn);
					}
				}
					
				selectCm.selectedIndex = 0;
				loadNodes();
			}
			
			private function assignAvailableIcon(item:Object):Class {
				var node:PhysicalNode = item as PhysicalNode;
				if(!this.hideUnavailableCheckBox.selected)
				{
					for each(var sn:SliceNode in this.sliceCanvas.allNodes)
					{
						if(!sn.node.isVirtual && sn.node.physicalNode == node)
							return DisplayUtil.notAvailableIcon;
					}
				}
				return DisplayUtil.assignAvailabilityIcon(item as PhysicalNode);
			}
			
			public function viewRspecs():void
			{
				if(sliceCanvas.slice.slivers.length == 0)
				{
					Alert.show("There is no valid RSPEC for the slice yet", "No RSPEC");
					return;
				}
				for each(var s:Sliver in sliceCanvas.slice.slivers)
				{
					if(s.rspec == null)
					{
						Alert.show("There is no valid RSPEC for the slice yet", "No RSPEC");
						return;
					}
				}
				var viewRspec:DefaultWindow = new DefaultWindow();
				viewRspec.title = "Last Valid Request RSPEC(s) for: " + sliceCanvas.slice.hrn;
				var tabs:TabNavigator = new TabNavigator();
				tabs.percentHeight = 100;
				tabs.percentWidth = 100;
				viewRspec.addChild(tabs);
				for each(s in sliceCanvas.slice.slivers)
				{
					var tab:VBox = new VBox();
					tab.percentHeight = 100;
					tab.percentWidth = 100;
					tab.label = s.componentManager.Hrn;
					var previewRspecText:TextArea = new TextArea();
					previewRspecText.percentHeight = 100;
					previewRspecText.percentWidth = 100;
					previewRspecText.text = s.rspec.toString();
					tab.addChild(previewRspecText);
					tabs.addChild(tab);
				}
				viewRspec.show();
			}
			
			public function previewRspecs():void
			{
				var previewRspec:DefaultWindow = new DefaultWindow();
				previewRspec.title = "Preview Request RSPEC(s) for: " + sliceCanvas.slice.hrn;
				var tabs:TabNavigator = new TabNavigator();
				tabs.percentHeight = 100;
				tabs.percentWidth = 100;
				previewRspec.addChild(tabs);
				for each(var s:Sliver in sliceCanvas.slice.slivers)
				{
					var tab:VBox = new VBox();
					tab.percentHeight = 100;
					tab.percentWidth = 100;
					tab.label = s.componentManager.Hrn;
					var previewRspecText:TextArea = new TextArea();
					previewRspecText.percentHeight = 100;
					previewRspecText.percentWidth = 100;
					previewRspecText.text = s.getRequestRspec().toString();
					tab.addChild(previewRspecText);
					tabs.addChild(tab);
				}
				previewRspec.show();
			}

		]]>
	</mx:Script>
	<mx:HBox width="100%">
		<mx:Label text="{sliceCanvas.slice.hrn}" fontSize="17" fontWeight="bold" selectable="true"/>
		<mx:Spacer width="100%"/>
		<mx:Label text="{sliceCanvas.slice.urn}" selectable="true"/>
		<display:ImageButton id="rspecButton"
							 source="@Embed('../images/page_white_code.png')"
							 click="viewRspecs()"
							 toolTip="View the most recent valid RSPEC" />
		<display:ImageButton id="previewRspecButton"
							 source="@Embed('../images/page_code.png')"
							 click="previewRspecs()"
							 toolTip="Preview the Request RSPEC" />
	</mx:HBox>
	<mx:HDividedBox width="100%" height="100%">
		<mx:VBox height="100%">
			<mx:ComboBox id="selectCm" width="100%" dataProvider="{cms}" change="loadNodes()"></mx:ComboBox>
			<mx:CheckBox label="Hide unavailable" selected="true" id="hideUnavailableCheckBox" click="loadNodes()"/>
			<mx:List width="100%" height="100%" id="listNodes"
					 dataProvider="{nodes}"
					 labelField="name" dragEnabled="true"
					 iconFunction="assignAvailableIcon">
			</mx:List>
			<mx:HBox width="100%">
				<mx:Button label="Exclusive"/>
				<mx:Button label="Shared"/>
			</mx:HBox>
		</mx:VBox>
			<display:SliceCanvas id="sliceCanvas" hostWindow="{this}">
			</display:SliceCanvas>
	</mx:HDividedBox>
	<mx:ControlBar>
		<display:ImageButton id="refreshButton" toolTip="Refresh slivers" width="16" height="16" click="Main.protogeniHandler.rpcHandler.refreshSlice(sliceCanvas.slice);" source="{DisplayUtil.refreshIcon}"/>
		<display:ImageButton id="createButton" toolTip="Create slivers" width="16" height="16" click="Main.protogeniHandler.rpcHandler.submitSlice(sliceCanvas.slice);" source="{DisplayUtil.acceptIcon}"/>
		<display:ImageButton id="deleteButton" toolTip="Delete slivers" width="16" height="16" click="Main.protogeniHandler.rpcHandler.deleteSlice(sliceCanvas.slice);" source="{DisplayUtil.deleteIcon}"/>
		<mx:VRule height="16"/>
		<display:ImageButton id="startButton" toolTip="Start slivers" width="16" height="16" click="Main.protogeniHandler.rpcHandler.startSlice(sliceCanvas.slice);" source="{DisplayUtil.playIcon}"/>
		<display:ImageButton id="stopButton" toolTip="Stop slivers" width="16" height="16" click="Main.protogeniHandler.rpcHandler.stopSlice(sliceCanvas.slice);" source="{DisplayUtil.stopIcon}"/>
		<display:ImageButton id="restartButton" toolTip="Restart slivers" width="16" height="16" click="Main.protogeniHandler.rpcHandler.restartSlice(sliceCanvas.slice);" source="{DisplayUtil.refreshIcon}"/>
		<mx:Button label="Embed"/>
	</mx:ControlBar>
	
</display:DefaultWindow>
