<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   minWidth="700" minHeight="400" xmlns:display="protogeni.display.*"
			   preinitialize="preinit();"
			   backgroundColor="#D2E1F0"
			   creationComplete="init();" xmlns:mapping="protogeni.display.mapping.*">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";
		@namespace mx "library://ns.adobe.com/flex/mx";
		@namespace display "protogeni.display.*";
		@namespace mapping "protogeni.display.mapping.*";
		
		.authenticatedStyle {
			themeColor: green;
			chromeColor: green;
		}
		
		.unauthenticatedStyle {
			themeColor: red;
			chromeColor: red;
		}
	</fx:Style>
	
	<fx:Script>
		<![CDATA[
			import com.google.maps.LatLng;
			import com.google.maps.MapOptions;
			import com.google.maps.MapType;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.controls.Image;
			import mx.controls.Label;
			import mx.controls.ToolTip;
			import mx.core.FlexGlobals;
			import mx.core.mx_internal;
			import mx.events.CloseEvent;
			import mx.events.CollectionEvent;
			import mx.events.ListEvent;
			import mx.events.ResizeEvent;
			import mx.managers.PopUpManager;
			import mx.managers.ToolTipManager;
			import mx.rpc.events.ResultEvent;
			
			import protogeni.GeniEvent;
			import protogeni.GeniHandler;
			import protogeni.Util;
			import protogeni.display.ConsoleWindow;
			import protogeni.display.DisplayUtil;
			import protogeni.display.GeniManagersWindow;
			import protogeni.display.SearchWindow;
			import protogeni.display.SlicesWindow;
			import protogeni.display.UserWindow;
			import protogeni.resources.Slice;
			import protogeni.resources.SliceAuthority;
			
			//------------------------------------------
			// Embedded resources
			[Embed(source="../images/arrow_out.png")] 
			private var arrowOut:Class;
			
			[Embed(source="../images/arrow_in.png")] 
			private var arrowIn:Class;
			
			public var console:protogeni.display.ConsoleWindow;
			public var cms:protogeni.display.GeniManagersWindow;
			public var about:AboutWindow = new AboutWindow();
			public var slices:protogeni.display.SlicesWindow;
			public var user:protogeni.display.UserWindow;
			public var search:protogeni.display.SearchWindow;
			
			[Bindable]
			public var allowAuthenticate:Boolean;
			
			[Bindable]
			public var slicesMenu : ArrayCollection;
			
			private var geniLocalSharedObject:SharedObject;
			
			public var forceMapKey:String;
			
			private function preinit():void {
				Security.allowDomain("*");
				Security.allowInsecureDomain("*");
				
				// Force a different Google Maps API key if given
				try{
					if(FlexGlobals.topLevelApplication.parameters.mapkey != null)
					{
						forceMapKey = FlexGlobals.topLevelApplication.parameters.mapkey;
					}
				} catch(all:Error) {
				}
			}
			
			private function init():void {
				
				Main.geniHandler = new GeniHandler();
				Main.geniHandler.mapHandler.map = map;
				Main.geniHandler.addEventListener(GeniEvent.SLICES_CHANGED, fillCombobox);
				Main.geniHandler.addEventListener(GeniEvent.USER_CHANGED, userChanged);
				
				// Default behavior (public)
				allowAuthenticate = true;
				Main.geniHandler.unauthenticatedMode = true;
				
				// Flash options
				loadParams();
				
				// Saved variables
				try {
					geniLocalSharedObject = SharedObject.getLocal("geniLocalSharedObject");
				} catch(e:Error) {
					Alert.show("It looks like Flash is unable to save/load local data.  Would you like to load directions on how to enable local data?", "Enable Local Data?", Alert.YES|Alert.NO, this,
						function allowData(e:CloseEvent):void {
							if(e.detail == Alert.YES) {
								Util.openWebsite("http://kb2.adobe.com/cps/408/kb408202.html");
							}
						});
				}
				loadLocalData();
				
				console = new protogeni.display.ConsoleWindow();
				PopUpManager.addPopUp(console, this, false);
				PopUpManager.centerPopUp(console);
				PopUpManager.removePopUp(console);
				Main.log = console;
				
				cms = new protogeni.display.GeniManagersWindow();
				cms.openFirst();
				cms.closeWindow();
				
				search = new protogeni.display.SearchWindow();
				search.showWindow();
				search.closeWindow();
				
				slices = new protogeni.display.SlicesWindow();
				slices.openFirst();
				slices.closeWindow();
				
				user = new protogeni.display.UserWindow();
				user.showWindow();
				user.load(Main.geniHandler.CurrentUser);
				user.closeWindow();
				
				Security.allowDomain("localhost");	
				
				// Load the certificate bundle
				var certRequest:URLRequest = new URLRequest("http://www.emulab.net/genica.bundle"); 
				var certLoader:URLLoader = new URLLoader(); 
				certLoader.addEventListener(Event.COMPLETE, certBundleResult);
				certLoader.addEventListener(IOErrorEvent.IO_ERROR, ioErrorHandler);
				certLoader.addEventListener(SecurityErrorEvent.SECURITY_ERROR, securityErrorHandler);
				try
				{
					Main.log.appendMessage(new LogMessage("http://www.emulab.net/", "Get Cert Bundle", "Getting the Protogeni certificate bundle", false, LogMessage.TYPE_START));
					certLoader.load(certRequest);
				}
				catch (e : Error)
				{
					Main.log.appendMessage(new LogMessage(url, "Error", "\n\nException on HTTP Call: "
						+ e.toString() + "\n\n", true));
				}
			}
			
			private function certBundleResult(e:Event):void
			{
				Main.certBundle = e.target.data;
				Main.log.appendMessage(new LogMessage("http://www.emulab.net/", "Get Cert Bundle", Main.certBundle, false, LogMessage.TYPE_END));
				Main.geniHandler.requestHandler.startInitiationSequence();
			}
			
			private function securityErrorHandler(event:SecurityErrorEvent):void {
				Main.certBundle = (new FallbackCertBundle()).toString();
				Main.log.appendMessage(new LogMessage("http://www.emulab.net/", "Get Cert Bundle", "Security error, using built-in cert bundle...\n" + Main.certBundle, true, LogMessage.TYPE_END));
				Main.geniHandler.requestHandler.startInitiationSequence();
			}
			
			private function ioErrorHandler(event:IOErrorEvent):void {
				Main.certBundle = (new FallbackCertBundle()).toString();
				Main.log.appendMessage(new LogMessage("http://www.emulab.net/", "Get Cert Bundle", "IO error, using built-in cert bundle...\n" + Main.certBundle, true, LogMessage.TYPE_END));
				Main.geniHandler.requestHandler.startInitiationSequence();
			}
			
			private function reinit(authenticateUser:Boolean = false):void
			{
				Main.geniHandler = new GeniHandler();
				Main.geniHandler.mapHandler.map = map;
				Main.geniHandler.addEventListener(GeniEvent.SLICES_CHANGED, fillCombobox);
				Main.geniHandler.addEventListener(GeniEvent.USER_CHANGED, userChanged);
				loadParams();
				loadLocalData();
				// Override the params
				Main.geniHandler.unauthenticatedMode = !authenticateUser;
				
				cms.init();
				user.load(Main.geniHandler.CurrentUser);
				slices.init();
			}
			
			public function showAbout():void {
				PopUpManager.addPopUp(about, this, false);
				PopUpManager.centerPopUp(about);
			}
			
			//------------------------------------------
			// GUI
			private function toggleFullScreen():void {
				try {
					switch (systemManager.stage.displayState) {
						case StageDisplayState.FULL_SCREEN:
							systemManager.stage.displayState = StageDisplayState.NORMAL;
							fullscreenButton.source = new arrowOut();
							break;
						default:
							systemManager.stage.displayState = StageDisplayState.FULL_SCREEN;
							fullscreenButton.source = new arrowIn();
							break;
					}
				} catch (err:SecurityError) {
					Alert.show("Fullscreen isn't supported on the current system.  Either you are running an older version of Fash or allowFullScreen is not set in the HTML object/embed tags.");
				}
			}
			
			public function userChanged(pe:GeniEvent):void {
				if(Main.geniHandler.CurrentUser == null
					|| Main.geniHandler.CurrentUser.credential == null
					|| Main.geniHandler.CurrentUser.credential.length == 0)
				{
					this.userButton.setStyle("icon", DisplayUtil.noUserIcon);
					this.userButton.toolTip = "Authenticate";
					this.userButton.label = "Authenticate";
				} else {
					this.userButton.setStyle("icon", DisplayUtil.userIcon);
					this.userButton.toolTip = "View user information"
					this.userButton.label = Main.geniHandler.CurrentUser.uid;
				}
			}
			
			public function showAuthenticate():void
			{
				if(!allowAuthenticate)
					return;
				
				userButton.styleName = "unauthenticatedStyle";
			}
			
			public function hideAuthenticate():void
			{
				if(!allowAuthenticate)
					return;
				
				userButton.styleName = "authenticatedStyle";
			}
			
			protected function loadUser():void
			{
				if(Main.geniHandler.CurrentUser == null
					|| Main.geniHandler.CurrentUser.credential == null
					|| Main.geniHandler.CurrentUser.credential.length == 0)
				{
					startover(true);
				} else {
					user.showWindow();
				}
			}
			
			// ComboBox
			public function fillCombobox(junk:* = null):void {
				slicesMenu = Main.geniHandler.CurrentUser.slices.displaySlices();
			}
			
			public function getSliceLabel(s:Slice):String {
				return s.DisplayString();
			}
			
			public function sliceSelected(evt:ListEvent):void {
				Main.geniHandler.mapHandler.selectedSlice = evt.currentTarget.selectedItem;
				onlyMyResourcesCheckbox.selected = Main.geniHandler.mapHandler.selectedSlice.hrn != null &&
					Main.geniHandler.mapHandler.selectedSlice.hrn.length > 0;
				Main.geniHandler.mapHandler.userResourcesOnly = onlyMyResourcesCheckbox.selected;
				Main.geniHandler.mapHandler.drawMap();
			}
			
			public function startover(authenticateUser:Boolean = false):void
			{
				Main.geniHandler.requestHandler.pause();
				Main.geniHandler.requestHandler.clearAll();
				reinit(authenticateUser);
				Main.geniHandler.requestHandler.startInitiationSequence();
			}
			
			public function loadParams():void
			{
				try{
					if(FlexGlobals.topLevelApplication.parameters.mode != null)
					{
						var input:String = FlexGlobals.topLevelApplication.parameters.mode;
						
						allowAuthenticate = input != "publiconly";
						Main.geniHandler.unauthenticatedMode = input != "authenticate";
					}
				} catch(all:Error) {
				}
				try{
					if(FlexGlobals.topLevelApplication.parameters.saurl != null)
					{
						for each(var sa:SliceAuthority in Main.geniHandler.GeniAuthorities.source) {
							if(sa.Url == FlexGlobals.topLevelApplication.parameters.saurl) {
								Main.geniHandler.forceAuthority = sa;
								break;
							}
						}
					}
				} catch(all:Error) {
				}
				try{
					if(FlexGlobals.topLevelApplication.parameters.publicurl != null)
					{
						Main.geniHandler.publicUrl = FlexGlobals.topLevelApplication.parameters.publicurl;
						Main.geniHandler.publicUrn = FlexGlobals.topLevelApplication.parameters.publicurn;
						Main.geniHandler.publicHrn = FlexGlobals.topLevelApplication.parameters.publichrn;
					}
				} catch(all:Error) {
				}
			}
			
			public function loadLocalData():void
			{
				if (geniLocalSharedObject != null && geniLocalSharedObject.size > 0)
				{
					for each(var sa:SliceAuthority in Main.geniHandler.GeniAuthorities.source) {
						if(sa.Url == geniLocalSharedObject.data.authority) {
							Main.geniHandler.CurrentUser.authority = sa;
							break;
						}
					}

					Main.geniHandler.CurrentUser.sslPem = geniLocalSharedObject.data.sslPem;
				}
			}
			
			public function saveLocalData():void
			{
				if(geniLocalSharedObject == null)
					return;
				
				geniLocalSharedObject.data.authority = Main.geniHandler.CurrentUser.authority.Url;
				geniLocalSharedObject.data.sslPem = Main.geniHandler.CurrentUser.sslPem;
				try
				{
					geniLocalSharedObject.flush();
				}
				catch (e:Error)
				{
					trace("Problem saving shared object");
				}
			}
		]]>
	</fx:Script>
	
	<s:controlBarContent>
		<display:ImageButton id="infoButton"
							 source="@Embed('../images/protogeni-logo.png')"
							 click="showAbout()"
							 toolTip="View more information about ProtoGENI" />
		<display:DefaultLine percentHeight="90" />
		<display:ImageButton id="refreshButton"
							 source="{DisplayUtil.refreshIcon}"
							 click="startover()" includeInLayout="false" visible="false"
							 toolTip="Reload all data" />
		<display:ImageButton id="cmButton"
							 source="@Embed('../images/building.png')"
							 click="cms.showWindow()"
							 toolTip="GENI managers"/>
		<display:ImageButton id="consoleButton"
							 source="@Embed('../images/application_xp_terminal.png')"
							 click="Main.log.open()" 
							 toolTip="Console"/>
		<display:DefaultLine percentHeight="90" />
		<display:ImageButton id="sliceButton" includeInLayout="{!Main.geniHandler.unauthenticatedMode}" visible="{!Main.geniHandler.unauthenticatedMode}"
							 source="{DisplayUtil.graphIcon}"
							 click="slices.showWindow()"
							 toolTip="Work with slices"/>
		<display:DefaultLine percentHeight="90" includeInLayout="{!Main.geniHandler.unauthenticatedMode}" visible="{!Main.geniHandler.unauthenticatedMode}" />
		<mx:ComboBox id="slicesCombobox" dataProvider="{slicesMenu}" labelFunction="getSliceLabel" change="sliceSelected(event)" includeInLayout="{!Main.geniHandler.unauthenticatedMode}" visible="{!Main.geniHandler.unauthenticatedMode}" />
		<s:CheckBox label="Show Mine Only" id="onlyMyResourcesCheckbox" click="Main.geniHandler.mapHandler.userResourcesOnly = onlyMyResourcesCheckbox.selected; Main.geniHandler.mapHandler.drawMap()" includeInLayout="{!Main.geniHandler.unauthenticatedMode}" visible="{!Main.geniHandler.unauthenticatedMode}" />
		<s:Rect percentWidth="100" />
		<s:Label text="Initialized" id="progressLabel"  fontStyle="normal" fontWeight="bold"/>
		<mx:SWFLoader id="waitingIcon" source="@Embed('../images/waiting.swf')" visible="false"/> 
		<s:Label text="" id="waitingCountLabel"  fontStyle="normal" fontWeight="bold"/>
		<s:Rect percentWidth="100" />
		<mx:Button icon="{DisplayUtil.noUserIcon}" click="loadUser()"
				   visible="{allowAuthenticate}" includeInLayout="{allowAuthenticate}"
				   toolTip="Authenticate" label="Authenticate" styleName="unauthenticatedStyle"
				   id="userButton" />
		<display:ImageButton id="searchButton"
							 source="{DisplayUtil.searchIcon}"
							 click="search.showWindow()"
							 toolTip="Search" />
		<display:ImageButton id="resetZoomButton"
							 source="@Embed('../images/zoom.png')"
							 click="map.resetZoom();"
							 toolTip="Reset zoom" />
		<display:ImageButton id="fullscreenButton"
							 source="@Embed('../images/arrow_out.png')"
							 click="toggleFullScreen()"
							 toolTip="Toggle fullscreen" />
		<display:ImageButton id="helpButton"
							 source="{DisplayUtil.helpIcon}"
							 click="Util.showManual()"
							 toolTip="View more information" />
	</s:controlBarContent>
	<mapping:GeniMap id="map" />
</s:Application>
