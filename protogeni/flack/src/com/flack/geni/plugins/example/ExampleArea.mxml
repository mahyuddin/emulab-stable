<?xml version="1.0" encoding="utf-8"?>
<plugins:PluginArea xmlns:fx="http://ns.adobe.com/mxml/2009" 
					xmlns:s="library://ns.adobe.com/flex/spark" 
					xmlns:mx="library://ns.adobe.com/flex/mx"
					xmlns:plugins="com.flack.geni.plugins.*"
					Title="Example">
	<fx:Script>
		<![CDATA[
			import com.flack.geni.resources.SliverTypes;
			import com.flack.geni.resources.virtual.LinkType;
			import com.flack.geni.resources.virtual.Sliver;
			import com.flack.geni.resources.virtual.VirtualLink;
			import com.flack.geni.resources.virtual.VirtualLinkCollection;
			import com.flack.geni.resources.virtual.VirtualNode;
			import com.flack.geni.resources.virtual.VirtualNodeCollection;
			import com.flack.shared.FlackEvent;
			import com.flack.shared.SharedMain;
			import com.flack.shared.utils.ImageUtil;
			import com.flack.shared.utils.StringUtil;
			
			public function insertDelayNodes():void
			{
				var changeLinks:VirtualLinkCollection = new VirtualLinkCollection();
				for each(var checkLink:VirtualLink in host.slice.links.collection)
				{
					if(checkLink.type.name == LinkType.LAN_V2
						&& checkLink.interfaceRefs.Interfaces.Nodes.getBySliverType(SliverTypes.DELAY).length == 0
						&& checkLink.interfaceRefs.length == 2)
					{
						changeLinks.add(checkLink);
					}
				}
				for each(var changeLink:VirtualLink in changeLinks.collection)
				{
					var newDelayNode:VirtualNode = new VirtualNode(
						host.slice,
						changeLink.interfaceRefs.collection[0].referencedInterface.Owner.manager,
						host.slice.getUniqueId(null, "delay"),
						true,
						SliverTypes.DELAY);
					var originalNodes:VirtualNodeCollection = changeLink.interfaceRefs.Interfaces.Nodes;
					newDelayNode.flackInfo.x = originalNodes.MiddleX;
					newDelayNode.flackInfo.y = originalNodes.MiddleY;
					host.slice.nodes.add(newDelayNode);
					
					changeLink.removeFromSlice();
					
					var leftLink:VirtualLink = new VirtualLink(host.slice);
					var leftLinkNodes:VirtualNodeCollection = new VirtualNodeCollection();
					leftLinkNodes.add(originalNodes.collection[0]);
					leftLinkNodes.add(newDelayNode);
					leftLink.establish(leftLinkNodes);
					host.slice.links.add(leftLink);
					
					var rightLink:VirtualLink = new VirtualLink(host.slice);
					var rightLinkNodes:VirtualNodeCollection = new VirtualNodeCollection();
					rightLinkNodes.add(originalNodes.collection[1]);
					rightLinkNodes.add(newDelayNode);
					rightLink.establish(rightLinkNodes);
					host.slice.links.add(rightLink);
					
					newDelayNode.cleanupPipes();
				}
				
				SharedMain.sharedDispatcher.dispatchChanged(
					FlackEvent.CHANGED_SLICE,
					host.slice,
					FlackEvent.ACTION_CHANGED
				);
			}
		]]>
	</fx:Script>
	
	<s:Button id="delayButton"
			  width="100%"
			  height="32"
			  label="Insert delay nodes"
			  toolTip="Insert delay nodes in the middle of all links"
			  icon="{ImageUtil.timeIcon}"
			  click="insertDelayNodes()" />
	
</plugins:PluginArea>
