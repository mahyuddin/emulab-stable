<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:display="protogeni.display.*">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			
			import protogeni.resources.DiskImage;
			import protogeni.resources.PhysicalLink;
			import protogeni.resources.PhysicalNode;
			import protogeni.resources.PhysicalNodeInterface;
			import protogeni.resources.SliverType;
			import protogeni.resources.VirtualLink;
			import protogeni.resources.VirtualLinkCollection;
			import protogeni.resources.VirtualNode;
			
			import spark.components.Button;
			import spark.primitives.Rect;
			
			[Bindable]
			public var node:PhysicalNode = null;
			
			[Bindable]
			public var virtualNode:VirtualNode = null;
			
			[Bindable]
			private var nodeTypes:ArrayCollection = null;
			
			[Bindable]
			private var sliverTypes:ArrayList;
			
			public function load(n:PhysicalNode):void {
				node = n;
				virtualNode = null;
				
				imgAvailable.source = DisplayUtil.assignAvailabilityIcon(n);
				if(n.virtualNodes != null && n.virtualNodes.length > 0)
					imgAvailable.toolTip = "Allocated, ";
				else
					if(n.available)
						imgAvailable.toolTip = "Available, ";
					else
						imgAvailable.toolTip = "Not available, ";
				if(n.exclusive)
					imgAvailable.toolTip += "Exclusive";
				else
					imgAvailable.toolTip += "Not exclusive";
				
				physicalLinksVbox.removeAllElements();
				
				var endpointArray:ArrayCollection = new ArrayCollection();
				var startpointArray:ArrayCollection = new ArrayCollection();
				
				if(Main.geniHandler.mapHandler.selectedSlice != null
					&& Main.geniHandler.mapHandler.selectedSlice.hrn != null
					&& Main.geniHandler.mapHandler.selectedSlice.hrn.length>0)
				{
					for each(var vn:VirtualNode in node.virtualNodes.collection)
					{
						if(vn.slivers.collection[0].slice == Main.geniHandler.mapHandler.selectedSlice)
						{
							virtualNode = vn;
							break;
						}
					}
				}
				
				sliverButton.visible = virtualNode != null;
				sliverButton.includeInLayout = virtualNode != null;

				nodeTypes = new ArrayCollection();
				for each(var nt:String in node.hardwareTypes)
					nodeTypes.addItem(nt);
				
				loadPhysicalLinks();
				loadSlices();
				loadHierarchy();
				
				sliverTypes = new ArrayList();
				for each(var sliverType:SliverType in node.sliverTypes) {
					for each(var diskImage:DiskImage in sliverType.diskImages) {
						var newSliverType:Object = new Object();
						newSliverType.name = sliverType.name;
						newSliverType.diskLongName = diskImage.name;
						newSliverType.diskShortName = DiskImage.getDiskImageShort(diskImage.name, node.manager);
						newSliverType.diskDescription = diskImage.description;
						newSliverType.diskOs = diskImage.os;
						newSliverType.diskVersion = diskImage.version;
						sliverTypes.addItem(newSliverType);
					}
				}
			}
			
			public function loadHierarchy():void
			{
				if(hierarchyVbox != null)
				{
					hierarchyVbox.removeAllElements();
					
					// Manager
					var managerHbox:HGroup = new HGroup();
					managerHbox.verticalAlign = "middle";
					managerHbox.gap = 4;
					managerHbox.addElement(DisplayUtil.getLabel("Hosted on"));
					managerHbox.addElement(DisplayUtil.getGeniManagerButton(node.manager));
					hierarchyVbox.addElement(managerHbox);
					
					// Parent node info
					if(node.subNodeOf != null)
					{
						var parentNodeHbox:HGroup = new HGroup();
						parentNodeHbox.verticalAlign = "middle";
						parentNodeHbox.gap = 4;
						parentNodeHbox.addElement(DisplayUtil.getLabel("Subnode of"));
						parentNodeHbox.addElement(DisplayUtil.getPhysicalNodeButton(node.subNodeOf));
						hierarchyVbox.addElement(parentNodeHbox);
					}
					
					// Child node info
					if(node.subNodes.length > 0)
					{
						var subNodesLabel:Label = new Label();
						hierarchyVbox.addElement(subNodesLabel);
						subNodesLabel.text = "Subnodes:";
						for each(var subNode:PhysicalNode in node.subNodes)
						{
							hierarchyVbox.addElement(DisplayUtil.getPhysicalNodeButton(subNode));
						}
					}
				}
			}
			
			public function loadSlices():void {
				if(sliceLinksVbox != null) {
					sliceLinksVbox.removeAllElements();
					if(virtualNode != null) {
						var connectedNodes:Vector.<PhysicalNode> = virtualNode.GetPhysicalNodes();
						
						sliceLinksNavigator.label = "Slice (" + connectedNodes.length + " Nodes)"
						var connectedLabel:Label = new Label();
						if(connectedNodes.length > 0)
							connectedLabel.text = "Connected on " + virtualNode.sliver.slice.hrn + " to";
						else
							connectedLabel.text = "No linked nodes on " + virtualNode.sliver.slice.hrn;
						sliceLinksVbox.addElement(connectedLabel);
						
						for each(var connectedNode:PhysicalNode in connectedNodes)
						{
							sliceLinksVbox.addElement(DisplayUtil.getPhysicalNodeButton(connectedNode));
							
							var connectedLinks:VirtualLinkCollection = virtualNode.GetLinksForPhysical(connectedNode);
							for each(var connectedLink:VirtualLink in connectedLinks.collection)
							{
								var pointLinkHbox:HGroup = new HGroup();
								var space:Rect = new Rect();
								space.width = 16;
								pointLinkHbox.addElement(space);
								pointLinkHbox.addElement(DisplayUtil.getVirtualLinkButton(connectedLink));
								sliceLinksVbox.addElement(pointLinkHbox);
							}
						}
					} else if(linkTabs.getChildren().length > 1)
						sliceLinksNavigator.visible = false;
				}
			}
			
			public function loadPhysicalLinks():void {
				if(physicalLinksVbox != null)
				{
					physicalLinksVbox.removeAllElements();
					var nodes:Vector.<PhysicalNode> = node.GetNodes();
					physicalLinksNavigator.label = "Physical (" + nodes.length + " Nodes)"
					var connectedLabel:Label = new Label();
					if(nodes.length > 0)
						connectedLabel.text = "Connected to";
					else
						connectedLabel.text = "No physically linked nodes";
					physicalLinksVbox.addElement(connectedLabel);
					
					for each(var n:PhysicalNode in nodes) {
						physicalLinksVbox.addElement(DisplayUtil.getPhysicalNodeButton(n));
						
						var nodeLinks:Vector.<PhysicalLink> = n.GetNodeLinks(node);
						for each(var l:PhysicalLink in nodeLinks) {
							var endInterface:PhysicalNodeInterface;
							for each(var testInterface:PhysicalNodeInterface in l.interfaceRefs.collection) {
								if(testInterface.owner == n) {
									endInterface = testInterface;
									break;
								}
							}
							
							var linkHbox:HGroup = new HGroup();
							var space:Rect = new Rect();
							space.width = 16;
							linkHbox.addElement(space);
							var endInterfaceButton:Button = DisplayUtil.getPhysicalLinkWithInterfaceButton(endInterface, l);
							linkHbox.addElement(endInterfaceButton);
							physicalLinksVbox.addElement(linkHbox);
						}
					}
				}
			}
			
			public function viewComponentRspec():void {
				DisplayUtil.viewXml(node.rspec, "Advertisement RSPEC for " + node.name);
			}
			
			public function viewSliverRspec():void {
				if(virtualNode == null)
					Alert.show("No Sliver RSPEC!");
				else {
					DisplayUtil.viewXml(virtualNode.rspec, "Request RSPEC for " + node.name);
				}
			}
		]]>
	</fx:Script>
	
	<s:HGroup width="100%" verticalAlign="middle">
		<s:Image id="imgAvailable" />
		<s:Label text="{node.name}" toolTip="{node.id}" fontSize="17" fontWeight="bold" width="100%" />
		<display:ImageButton id="locateButton"
							 icon="{ImageUtil.mapIcon}"
							 click="Main.geniHandler.mapHandler.zoomToPhysicalNode(node)"
							 toolTip="View Advertisement RSPEC" />
		<display:ImageButton id="componentButton"
							 icon="{ImageUtil.pageWhiteCodeIcon}"
							 click="viewComponentRspec()"
							 toolTip="View Advertisement RSPEC" />
		<display:ImageButton id="sliverButton"
							 icon="{ImageUtil.pageCodeIcon}"
							 click="viewSliverRspec()"
							 toolTip="View Request RSPEC" visible="false" />
	</s:HGroup>
	<s:HGroup width="100%">
		<s:Label text="Types" fontWeight="bold" />
		<s:List id="typeList" dataProvider="{nodeTypes}" labelField="name" height="68" />
		<s:VGroup width="100%">
			<s:Label text="Hierarchy" fontWeight="bold"/>
			<s:VGroup width="100%" height="100%" gap="4" id="hierarchyVbox" />
		</s:VGroup>
	</s:HGroup>
	
	<s:Label text="Sliver Types / Disk Images" fontWeight="bold" />
	<s:DataGrid id="sliverGrid" dataProvider="{sliverTypes}" width="100%" height="106">
		<s:columns>
			<s:ArrayList>
				<s:GridColumn dataField="name" headerText="Sliver Type"/>
				<s:GridColumn headerText="OSID">
					<s:itemRenderer>
						<fx:Component>
							<s:GridItemRenderer>
								<s:Label text="{data.diskShortName}" toolTip="{data.diskLongName}"
										 maxDisplayedLines="1"
										 left="10" right="5" paddingTop="10" paddingBottom="5"/>
							</s:GridItemRenderer>
						</fx:Component>
					</s:itemRenderer>
				</s:GridColumn>
				<s:GridColumn headerText="Description">
					<s:itemRenderer>
						<fx:Component>
							<s:GridItemRenderer>
								<s:Label text="{data.diskDescription}" toolTip="{data.diskOs} {data.diskVersion}"
										 maxDisplayedLines="1"
										 left="10" right="5" paddingTop="10" paddingBottom="5"/>
							</s:GridItemRenderer>
						</fx:Component>
					</s:itemRenderer>
				</s:GridColumn>
			</s:ArrayList>
		</s:columns>
	</s:DataGrid>
	
	<s:HGroup width="100%" height="100%">
		<s:Label text="Links to" fontWeight="bold"/>
		<mx:TabNavigator id="linkTabs" width="100%" height="100%" paddingTop="2" paddingRight="2" paddingLeft="2" paddingBottom="2">
			<s:NavigatorContent label="Physical" width="100%" height="100%" id="physicalLinksNavigator">
				<s:Scroller width="100%" height="100%">
					<s:VGroup id="physicalLinksVbox" width="100%" height="100%" />
				</s:Scroller>
			</s:NavigatorContent>
			<s:NavigatorContent label="Slice" width="100%" height="100%" id="sliceLinksNavigator">
				<s:Scroller width="100%" height="100%">
					<s:VGroup id="sliceLinksVbox" width="100%" height="100%" />
				</s:Scroller>
			</s:NavigatorContent>
		</mx:TabNavigator>
	</s:HGroup>
	
</s:VGroup>
