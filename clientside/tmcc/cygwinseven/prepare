#!/usr/bin/perl -w
#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2005 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
#require 'ctime.pl';

#
# Prepare the node for cutting a new image.  Should be run just prior.
#

# Turn off line buffering on output
$| = 1;

# Drag in path stuff so we can find emulab stuff.
BEGIN { 
    require "/etc/emulab/paths.pm"; 
    import emulabpaths; 
    $ENV{'PATH'} .= ":/cygdrive/c/Windows/System32:/cygdrive/c/Windows";
}

#
# Load the OS independent support library. It will load the OS dependent
# library and initialize itself. 
# 
use libsetup;
use librc;

sub usage()
{
    print("Usage: " . scriptname() . " [-p Rootpwd] [-n] [-s] [-c] [-d]\n" .
	  "    -p Rootpwd : Root password, for Emulab services redef.\n" .
	  "        If not present, and not -n, will be prompted-for.\n" .
	  "    -n : Root password was not changed, don't redef services.\n" .
	  "    -s : Make a hardware-independent image using Sysprep.\n");
    exit(1);
}
my $optlist = "p:ns";
my $rootpwd = "";
my $rootpwd2 = "";
my $noredef = 0;
my $sysprep = 0;

# Parse command line.
use Getopt::Std;
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{'p'})) {
    $rootpwd = $rootpwd2 = $options{'p'};
}
if (defined($options{'n'})) {
    $noredef = 1;
}
if (defined($options{'s'})) {
    $sysprep = 1;
}

# Only root.
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be root to run this script!\n");
}

# Note: No /etc/dumpdates support on CygWin, and no /etc/fstab or /var/account either.
my @DBFILES     = ();
my @VARDIRS	= ("logs", "db", "jails", "boot", "lock", "lib/dhcp");
my $LOGDIR      = "/var/log";
my $RUNDIR      = "/var/run";
my $SOURCES     = "/home/root/flux/testbed";
my $OBJS        = "/home/root/flux/obj-real";
my $HISTORY     = "/home/root/.bash_history";
my $MAILFILE    = "/var/spool/mail/root";
my $NTPDRIFT    = "/etc/ntp/drift";

#
# Dead wood in $BINDIR
# If you remove/move a script, list it here so that it goes away when the
# image is remade.
#
my @DEADFILES	= ("rc.agents", "rc.delayagent", "rc.delta", "rc.healthd",
		   "rc.injail", "rc.ipod", "rc.mfs", "rc.progagent",
		   "rc.setup", "rc.slothd", "rc.testbed",
		   "batchcmddone", "bootsetup", "install-tarfile",
		   "jailsetup", "update_delays", "rc/rc.linux",
		   "sethostname", # replaced by sethostname.dhclient on linux
		   "startssh", "stopssh", "rc/rc.cygwinxp-user"
		   );

sub do_cmd ($)
{
    my($cmd) = @_;

    print "  $cmd\n"; 
    system($cmd);
}

my $windir = "/cygdrive/c/WINDOWS";
my $sysdir = "$windir/system32";
my $netcmd = "$sysdir/net.exe";

# Could use /share/windows/sysprep, but prepare may have been run once
# already, and unmounted /share.  We mount Z: to authenticate, so use that.
my $sysprep_src = "/cygdrive/z/windows/sysprep";

my $sysprep_dst = "/cygdrive/c/sysprep";
my $sysprep_cmd = "$sysprep_dst/sysprep.exe";

if (! $noredef) {
    #
    # Need the current root password to re-define the Emulab services to match.
    # (May already have one from a -p argument.  Prompt if not.)
    #
    use Term::ReadKey;
    while ($rootpwd eq "") {
	print "Enter the root password: ";
	ReadMode 'noecho';
	$rootpwd = ReadLine 0;
	chomp $rootpwd;
	ReadMode 'normal';
	print "\n";
    }
    while ($rootpwd2 eq "" || $rootpwd ne $rootpwd2) {
	print "Didn't match, try again.\n"
	    if $rootpwd2 ne "" && $rootpwd ne $rootpwd2;
	print "Re-enter the root password: ";
	ReadMode 'noecho';
	$rootpwd2 = ReadLine 0;
	chomp $rootpwd2;
	ReadMode 'normal';
	print "\n";
    }

    #
    # Set the root password to make sure it matches.  We also set this to be
    # the Administrator password, to match the sysprep.inf file entry below.
    #print "\nSetting root password to $rootpwd.\n";
    #system("echo '$rootpwd\n$rootpwd\n' | passwd root");
    #print "\nSetting Administrator password to $rootpwd.\n";
    #system("echo '$rootpwd\n$rootpwd\n' | passwd Administrator");

    #
    # Windows stores the password as part of the definition of services that
    # run as a real user, such as root, rather than as SYSTEM.  Redefine them
    # with the current password.  Otherwise, Windows will refuse to start them
    # up at reboot.
    #
    print "\nRedefining EmulabShutdown.\n";
    # Have to remove a service before you can redefine it.
    system("cygrunsrv -R EmulabShutdown");
    system("cygrunsrv -I EmulabShutdown -u root -w '$rootpwd'" .
	   " -p /cygdrive/c/cygwin/bin/bash" .
	   " --shutdown --type manual" .
	   " -a \"--norc --noprofile -c '/usr/local/etc/emulab/tbshutdown'\"");
    system("cygrunsrv -VQ EmulabShutdown");
    print "\nRedefining EmulabStartup.\n";
    my $firstboot = "/usr/local/etc/emulab/rc/rc.firstboot";
    my $bootsetup = "/usr/local/etc/emulab/rc/rc.bootsetup";
    my $sshrun = "cygrunsrv -S sshd";
    my $progrun = "cygrunsrv -S ProgAgent";
    my $bootlog = "/var/log/bootsetup.log";
    system("cygrunsrv -R EmulabStartup");
    system("cygrunsrv -I EmulabStartup -u root -w '$rootpwd'" .
	   " --dep DHCP --dep iphlpsvc --dep pubsub --dep W32Time" .
	   " -p /cygdrive/c/cygwin/bin/bash" .
	   " -a \"--norc --noprofile -c " .
	   "      '( $bootsetup; $progrun ) >& $bootlog'\"");
#          "      '( $firstboot; $bootsetup; $sshrun; $progrun ) >& $bootlog'\"");

    system("cygrunsrv -VQ EmulabStartup");
    system("sc config EmulabStartup start= delayed-auto");
}

# Capture the node name before cleaning out the config files.
my $vname = "";
if (open(NICK, TMNICKNAME)) {
    $vname = <NICK>;
    chomp $vname;
    $vname =~ s/\..*//;
    close(NICK);
}

#
# First clean up the node as it would be if free.
#
print "\n";
cleanup_node(1);

print "Running $BINDIR/rc/rc.config to clean up ...\n";
system("$BINDIR/rc/rc.config reset");

print "Removing old DB files ...\n";
foreach my $dbfile (@DBFILES) {
    if (-e $dbfile) {
	unlink($dbfile) or
	    die("Could not unlink $dbfile");
    }
}

print "Clearing Cygwin fstab";
system("cp /dev/null /etc/fstab");

# Stop anything holding a logfile open which we will want to remove below.
print "Stopping the watchdog ...\n";
system("$BINDIR/watchdog stop");

print "Stopping the program-agent ...\n";
system("$BINDIR/rc/rc.progagent shutdown");

print "Stopping emulab-syncd ...\n";
system("$BINDIR/rc/rc.syncserver shutdown");

print "Stopping linktest agent...\n";
system("$BINDIR/rc/rc.linktest stop");

print "Stopping slothd ...\n";
system("$BINDIR/rc/rc.slothd stop");
my $pidfile = "/var/run/slothd.pid";
unlink($pidfile) or
    warn("Could not unlink $pidfile");

print "Stopping evproxy ...\n";
system("killall evproxy");
$pidfile = "/var/run/evproxy.pid";
unlink($pidfile) or
    warn("Could not unlink $pidfile");

print "Stopping performance counter";
system("logman stop ldavg");
my $perflog = "/var/run/ldavg.csv";
unlink($perflog)
    or warn("Could not unlink $perflog");

print "Stopping idlemon\n";
system("killall idlemon");
my $idlemon_activity_file = "/var/run/rdp_input";
unlink($idlemon_activity_file)
    or warn("Could not unlink $idlemon_activity_file");

# Logfiles.
print "Cleaning logfiles ...\n";
opendir(LOG,$LOGDIR) or
    die("Could not open directory $LOGDIR: $!");

while ($dirent = readdir(LOG)) {
    my $file = $LOGDIR . "/" . $dirent;
    if (-f $file) {
        if (($file =~ /\.\d$/) || ($file =~ /\.\d\.gz$/)) {
            # Remove archived logfiles
            unlink($file) or
                die("Could not unlink $file: $!");
        } elsif (-s $file) {
            # Truncate other logfiles - many do not get re-created
            truncate($file,0) or
		system("chown root $file") and system("chmod 666 $file") 
		    and truncate($file,0) or
			warn("Could not truncate $file: $!");
        }
    }
}
closedir(LOG);

print "Removing root's history ...\n";
if (-f $HISTORY) {
    unlink($HISTORY) or
        die("Could not unlink $HISTORY: $!");
}

print "Removing root's mailfile ...\n";
if (-f $MAILFILE) {
    unlink($MAILFILE) or
        die("Could not unlink $MAILFILE: $!");
}

#
# Just removing the drift file gives an error, so set it to zero instead.
# Watchdog never sends zero drift files to the DB so we lose nothing.
#
print "Resetting drift in $NTPDRIFT ...\n";
if (-f $NTPDRIFT) {
    open(DRIFT, ">$NTPDRIFT") or
	die("Could not open $NTPDRIFT: $!");
    print DRIFT "0\n";
    close(DRIFT);
}

print "Clearing out testbed sources $SOURCES...\n";
system("rm -rf $SOURCES");

print "Clearing out testbed sources $OBJS...\n";
system("rm -rf $OBJS");

print "Clearing out $RUNDIR ...\n";
system("rm -rf $RUNDIR/*.pid");

print "Clearing out /tmp ...\n";
system("chmod -Rf 777 /tmp/* /tmp/.??* >& /dev/null");
system("chown -Rf root /tmp >& /dev/null");
system("rm -rf /tmp/* /tmp/.??*");

print "Cleaning out /local/logs ...\n";
system("chown -R root /local/logs");
system("rm -rf /local/logs/* /local/logs/.??*");

#print "Cleaning out Windows logs ...\n";
#system("chown root C:/windows/iis6.log");
#system("rm -f C:/windows/iis6.log");

print "Clearing out C:/TEMP ...\n";
system("chown -R root C:/TEMP");
system("rm -rf C:/TEMP/* C:/TEMP/.??*");

print "Clearing out directories in $VARDIR ...\n";
foreach my $dir (@VARDIRS) {
    if (-d "$VARDIR/$dir") {
	system("rm -rf $VARDIR/$dir/*");
    }
}

print "Clearing out old Emulab scripts and binaries in $BINDIR ...\n";
foreach my $file (@DEADFILES) {
    if (-f "$BINDIR/$file") {
	unlink("$BINDIR/$file") or
	    warn("*** could not remove $BINDIR/$file\n");
    }
}

print "Removing backup files in /etc\n";

opendir(ETC,"/etc") or
    die ("Couldn't open /etc: $!");

while ($dirent = readdir(ETC)) {
    my $file = "/etc/" . $dirent;
    if (-f $file) {
	if (($file =~ /~$/) || ($file =~ /-$/)) {
	    unlink($file) or
		die ("Couldn't unlink $file: $!");
	}
    }
}
closedir(ETC);

print "Updating /etc/motd.\n";
if (open(MOTD, ">/etc/motd")) {
    open(UNAME, "uname -sr |");
    my $uname = <UNAME>;
    chomp $uname;
    close(UNAME);
    printf MOTD "%s, Windows-7 + %s, %s\n", $vname, $uname, scalar(localtime());
    print MOTD "\n\nWelcome to Windows in Emulab!\n\n";
    close(MOTD);
}
else {
    warn("Could not open /etc/motd!\n");
}

# XXX: Fix up to clean the right way under win7
#print "Cleaning out sp2 & Windows Update dirs.\n";
#system("rm -f /tmp/WindowsXP-KB835935-SP2-ENU.exe");
#system("du -sm C:/WINDOWS/\\\$*");
#system("rm -rf C:/WINDOWS/\\\$*");
#system("du -sm C:/WINDOWS/ServicePackFiles/i386");
#system("rm -rf C:/WINDOWS/ServicePackFiles/i386");
#system("du -sm C:/WINDOWS/SoftwareDistribution/Download");
#system("rm -rf C:/WINDOWS/SoftwareDistribution/Download/*");

print "\nRe-enabling all network interfaces.\n";
system("devcon enable =net '*PCI*'");

#
# Run sysprep.
#
if ($sysprep) {
    my $sp_path = "/cygdrive/c/Windows/System32/sysprep";

    # Redefine root password in unattend.xml if necessary
    if ($rootpwd) {
	print "Modifying root password in unattend.xml\n";
	open(UNANEW, ">$sp_path/unattend-new.xml")
	    or die "Can't open new unattend file. Sysprep aborted.";
	open(UNAORIG, "<$sp_path/unattend-src.xml")
	    or die "can't open existing unattend.xml.  Sysprep aborted.";
	while (my $uline = <UNAORIG>) {
	    if ($uline =~ /<Password>/) {
		my $dummy = <UNAORIG>, <UNAORIG>; # zap next two lines.
		$uline .= " "x28 . "<Value>$rootpwd</Value>\r\n" .
		    " "x28 . "<PlainText>true</PlainText>\r\n";
	    }
	    print UNANEW $uline;
	}
	close(UNANEW);
	close(UNAORIG);
	rename("$sp_path/unattend-new.xml", "$sp_path/unattend.xml") or
	    die "Can't move new unattend.xml file into place.  sysprep aborted."
    } else {
	print "Not modifying root password in unattend.xml - assuming it's the same or modified outside of this script...\n";
    }

    # Clean out sysprep work/log areas
    system("rm -rf /cygdrive/c/Windows/Panther/*");
    system("rm -rf $sp_path/Panther/*");
    chdir $sp_path;
    print "\nExecuting sysprep.  The system will shutdown shortly.  Wait a bit before trying to capture an image (ping the host - wait a minute after it stops).\n";
    system("./sysprep.exe /quiet /oobe /generalize /shutdown /unattend:unattend.xml");
} else {
    print "\nDone!  Now capture the Windows disk image for:\n";
    system("head -1 /etc/motd");
}
