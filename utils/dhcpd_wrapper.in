#!/usr/bin/perl -w
#
# EMULAB-COPYRIGHT
# Copyright (c) 2004, 2005 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
use Getopt::Std;
use POSIX qw(setsid);

sub usage()
{
    print "Usage: dhcpd_wrapper\n";
    exit(1);
}
my $optlist  = "";
my $debug    = 0;

my $TBOPS      = "@TBOPSEMAIL@";
my $PIDFILE    = "/var/run/dhcpd_wrapper.pid";
my $LOGFILE    = "/var/log/dhcpd_wrapper.log";
my $DHCPD      = "/usr/local/sbin/dhcpd -f ";
#my $DHCPDOPTS = "-cf /usr/local/etc/dhcpd.conf.v3 fxp0";
my $DHCPDOPTS  = "";
my $childpid;

# Turn off line buffering on output
$| = 1;

# Only root.
if ($UID != 0) {
    die("*** $0:\n".
	"    Must be root to run this script!\n");
}

#
# Testbed support libs.
# 
use lib "@prefix@/lib";
use libtestbed;

# Signal handler to initiate cleanup in parent and the children.
sub Pcleanup($)
{
    my ($signame) = @_;

    $SIG{TERM} = 'IGNORE';

    if (defined($childpid)) {
	system("kill $childpid");
	waitpid($childpid, 0);
    }
    unlink $PIDFILE;
    exit(0);
}

# Daemonize;
if (!$debug && TBBackGround($LOGFILE)) {
    sleep(1);
    exit(0);
}

#
# Write our pid into the pid file so we can be killed later. 
#
system("echo '$PID' > $PIDFILE") == 0 or
    die("*** $0:\n".
	"    Could not create $PIDFILE!");

# Okay, cleanup function.
$SIG{TERM} = \&Pcleanup;

# Fully disconnect from bootup. 
setsid();

# Loop forever, restarting DHCPD if it ever dies.
while (1) {
    $childpid = fork();

    die("*** $0:\n".
	"    Could not fork!\n")
	if ($childpid < 0);

    if ($childpid == 0) {
	exec("$DHCPD $DHCPDOPTS");
	die("*** $0:\n".
	    "    Could not exec $DHCPD!\n");
    }
    waitpid($childpid, 0);

    if (($? >> 8) == 1) {
	SENDMAIL($TBOPS, "DHCPD died",
		 "$DHCPD died with 1\n".
		 "Not restarting it; might be a config file error",
		 $TBOPS);
	unlink $PIDFILE;
	die("*** $0:\n".
	    "    Could not start $DHCPD! Might be a config file error.\n");
    }
    #
    # Anytime it exits, send email (unless TERM).
    #
    if ($? != 15) {
	SENDMAIL($TBOPS, "DHCPD died",
		 "$DHCPD died with $?; restarting it.",
		 $TBOPS);
    }
    sleep(1);
}
exit(0);

