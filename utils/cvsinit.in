#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2005, 2006 University of Utah and the Flux Group.
# All rights reserved.
#
use English;

#
# Initialize the CVS support.
#
sub fatal($);

#
# Configure variables
#
my $TB       = "@prefix@";
my $CVSBIN   = "/usr/bin/cvs";
my $CHOWN    = "/usr/sbin/chown";
my $CVSSUPPORT  = @CVSSUPPORT@;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/usr/bin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1;

#
# Load the Testbed support stuff. 
#
use lib "@prefix@/lib";
use libaudit;
use libdb;
use libtestbed;

#
# We have to run this script as root.
#
if ($UID != 0) {
    die("*** $0:\n".
	"    Must run this script as root!\n");
}

if (! $CVSSUPPORT) {
    die("*** $0:\n".
	"    You must turn on CVSSUPPORT in your defs file!\n");
}

my $query_result =
    DBQueryFatal("select pid,head_uid from projects");

my $PROJROOT = PROJROOT();
while (my ($pid,$projhead) = $query_result->fetchrow_array()) {
    next
	if (! -d "$PROJROOT/$pid");

    my $CVSDIR = "$PROJROOT/$pid/CVS";

    next
	if (-d $CVSDIR && -d "$CVSDIR/CVSROOT");

    my (undef,undef,$uid) = getpwnam($projhead)
	or fatal("$projhead not in passwd file");

    my (undef,undef,$gid) = getgrnam($pid)
	or fatal("$pid not in group file");

    if (! -e $CVSDIR) {
	if (! mkdir("$CVSDIR", 0770)) {
	    fatal("Could not make directory $CVSDIR: $!");
	}
	if (! chmod(0770, "$CVSDIR")) {
	    fatal("Could not chmod directory $CVSDIR: $!");
	}
	if (! chown($uid, $gid, "$CVSDIR")) {
	    fatal("Could not chown $CVSDIR to $uid/$gid: $!");
	}
    }
    system("$CVSBIN -d $CVSDIR init");
    if ($?) {
	fatal("Could not cvs init $CVSDIR!");
    }
    # Chown the tree.
    system("$CHOWN -R ${uid}:${gid} $CVSDIR");
    if ($?) {
	fatal("Could not chown ${uid}:${gid} $CVSDIR!");
    }
}
exit(0);

sub fatal($) {
    my($mesg) = $_[0];

    die("*** $0:\n".
	"    $mesg\n");
}
