#!/usr/local/bin/perl -w
use Getopt::Std;
#
# vlandiff - Show differences between switch state and vlan table, if any
#

#
# Configure variables
#
$ENV{'PATH'} = '@prefix@/sbin:@prefix@/bin:/bin';
use lib '@prefix@/lib';
use libdb;

my %opt = ();
getopts('v',\%opt);

my $debug = 0;
if ($opt{v}) {
    $debug = 1;
}

if (@ARGV) {
    die "Usage: $0 [-v]\n";
}

my %table = ();
my %switch= ();
my %id = ();

# Get all the vlans from the table

my $result = DBQueryFatal("select id,members from vlans");
while (@row = $result->fetchrow_array()) {
    my @list = split(" ",$row[1]);
    foreach $port (@list) {
	my ($node,$card) = split(":",$port);
	if ($card =~ /[a-zA-Z]/) {
	    # specified ala ethX
	    my $result2 = DBQueryFatal("select card from interfaces ".
		    "where node_id='$node' and iface='$card'");
	    $card = ($result2->fetchrow_array())[0];
	    if ($debug) { print "Had '$port', changed to '$node:$card'\n"; }
	    $port = "$node:$card";
	}
    }
    $table{$row[0]} = join(" ",sort @list);
    if ($debug) { print "In table: vlan $row[0]: $table{$row[0]}\n"; }
}

# Get all the vlans from the switch

my $list;
my $vlan=0;
open(LIST,"snmpit -l |") || die ("vlandiff: couldn't run snmpit: $!\n");
while(<LIST>) {
    chop;
    if (/(^ID)|(^--)|(^1 )/) { next; }
    s/[\t ]+/ /g;
    if ( /^(\d+)\s+(\S+)(?:\s+(.*))?$/ ) {
	if (!defined $3) { $list = ""; } else { $list = $3; }
	if ($2 ne "default") {
	    $switch{$2} = join(" ", sort split(" ",$list));
	    $id{$2} = $1;
	    $vlan = $2;
	    if ($debug) { print "On switch: vlan #$1 $2: $switch{$2}\n"; }
	}
    } elsif ( /^\s+(.*)?$/ ) {
	if (!defined $1) { $list = ""; } else { $list = $1; }
	$switch{$vlan} .= " ".join(" ", sort split(" ",$list));
	if ($debug) { print "On switch: vlan #$id{$vlan} $vlan: $switch{$vlan}\n"; }
    }
}
close(LIST);

# Compare all of them...

foreach $key (sort keys %table) {
    if ( defined $switch{$key} && ($switch{$key} eq $table{$key}) ) {
	# If its in both lists, delete it from both
	if ($debug) {
	    print "vlan table:$key ($table{$key}) matches switch:$key ($switch{$key})\n"
	}
	delete $switch{$key};
	delete $table{$key};
    }
}

# Print out the difference

my $rv = 0;

if (keys %table) {
    print "In vlans table only:\n";
    foreach $key(sort keys %table) {
	print "$key\t$table{$key}\n";
    }
    $rv++;
}

if (keys %switch) {
    print "On switch only:\n";
    foreach $key(sort keys %switch) {
	print "#",$id{"$key"},": $key\t$switch{$key}\n";
    }
    $rv++;
}

if ($debug) {
    print "Exiting with value $rv\n";
}


# Return 0 if same, 1 for a one-sided diff, 2 for a two-sided diff
# (Could be changed to return total # of different vlans)
exit($rv);
