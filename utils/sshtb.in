#!/usr/bin/perl -w

#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2011 University of Utah and the Flux Group.
# All rights reserved.
#

use English;

#
# An ssh frontend to determine if the node is local or remote, and to
# add in special options.
#
# NOTE THE DIFFERENT SYNTAX!
# 

sub usage()
{
    print STDOUT
	"Usage: sshtb [ssh args] -host <hostname> [command and args ...]\n";
    exit(-1);
}

#
# Configure variables
#
my $TB		= "@prefix@";
my $SSH	        = "@SSH@";
my $SSH_ARGS	= '@SSH_ARGS@';
my $BOSSNODE    = "@BOSSNODE@";
my $USERNODE    = "@USERNODE@";
my $FSNODE      = "@FSNODE@";
my $MAINSITE    = @TBMAINSITE@;

# Locals
my $debug	= 0;
my @args;
my $hostname;
my $chpid       = 0;
# Run command on the local machine.
my $runlocal    = 0;

#
# Testbed Support libraries
#
use lib "@prefix@/lib";
use libdb;
use libtestbed;
use Node;

#
# Turn off line buffering on output
#
$| = 1;

# un-taint path
$ENV{'PATH'} = "/bin:/usr/bin:/usr/local/bin:$TB/libexec:$TB/sbin:$TB/bin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# We run through the args looking for -host. We save everything else.
#
if (@ARGV < 2) {
    usage();
}

# Find everything before the -host.
while (@ARGV) {
    my $arg = shift(@ARGV);
    if ($arg eq "-host") {
	$hostname = shift(@ARGV);
	last;
    }
    push(@args, $arg);
}

if (!defined($hostname)) {
    usage();
}

#
# Different stuff for remote nodes.
#
# Special case: When asking to do something on the the FSNODE, and the
# FSNODE is actually BOSSNODE, run it locally.
#
my @cmdargs;

if ($hostname eq $FSNODE && $FSNODE eq $BOSSNODE) {
    @cmdargs  = "@ARGV";
    $runlocal = 1;
}
else {
    my $user;
    my $node = Node->Lookup($hostname);

    if (defined($node)) {
	if ($node->isvirtnode()) {
	    if ($node->isplabdslice()) {
		TBPlabNodeUsername($hostname, \$user);
                # if the node didn't exist in the db, we don't want to just
                # try logging in as root!
                if (!defined($user) || "$user" eq "0") {
                    exit(1);
                }
	    }
	    $hostname = $node->phys_nodeid();
	}
	elsif ($node->isremotenode() && !$node->isdedicatedremote()) {
	    $user = "emulabman";
	}
    }

    #
    # Yuck, the point is to turn the above string into a proper list for
    # exec so that we do not get a shell to interpret the arguments cause
    # there are quotes embedded. I am sure there is a regex that will do this
    # for me, but hell if I know what it is.
    #
    my @sshargs = ();
    my $tmp;
    foreach my $f (split('\s+', $SSH_ARGS)) {
	if (defined($tmp)) {
	    if ($f =~ /(.*)\"$/) {
		$tmp = "$tmp $1";
		push(@sshargs, $tmp);
		undef($tmp);
	    }
	    else {
		$tmp = "$tmp $1";
	    }
	    next;
	}
	elsif ($f =~ /^\"(.*)$/) {
	    $tmp = $1;
	    next;
	}
	push(@sshargs, $f);
    }

    @cmdargs = (@sshargs, @args,
		(defined($user) ? ("-l", "$user") : ()),
		$hostname, @ARGV);
}

if ($debug) {
    print "@cmdargs\n";
}
# Close our connection the DB to avoid holding open connections.
TBDBDisconnect();

#
# Signal Helper - help reap child process
#
sub sighandler {
    kill("TERM", $chpid);
    my $kpid;
    do {
        $kpid = wait();
    } until $kpid == -1;
    exit $?;
}

#
# Run command locally.
#
if ($runlocal) {
    exec(@cmdargs);
}
#
# Check for existing protocol specification args.
# If they exist, don't set the protocol.
#
elsif (grep {/Protocol/ || /-[12]/} @args) {
    print "Protocol spec present on command line - not forcing it.\n" 
        if $debug;
    exec($SSH, @cmdargs);
} else {
    #
    # Try both ssh protocol 2 and 1 for backward compatibility with
    # old images utilizing only v1 keys.
    #
    print "Trying ssh protocol 2...\n" if $debug;
    if ($chpid = fork()) {
        # Yuck.  Must deal with termination signals (kill child proc)
        local $SIG{TERM} = \&sighandler;
        local $SIG{HUP}  = \&sighandler;
        local $SIG{INT}  = \&sighandler;
        my $kidpid = waitpid($chpid, 0);
        # Sanity check.
        if ($kidpid < 1) { 
            warn "*** $0: waitpid() returned $kidpid.  Exiting.";
            sighandler();
        }
        my $kidstatus = $? >> 8;
        if ($kidstatus == 255) {
            # XXX: May not be due to v2 vs. v1, but there is no
            #      way to differentiate from the exit value.
            print "Protocol 2 failed:  Trying ssh protocol 1\n" if $debug;
            exec($SSH, "-o Protocol=1", @cmdargs);
        } else {
            exit $kidstatus;
        }
    } else {
        exec($SSH, "-o Protocol=2", @cmdargs);
    }
}

